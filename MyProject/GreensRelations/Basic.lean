import MyProject.GreensRelations.Decidable

/-!
# Basic Properties of Green's Relations

This file establishes fundamental properties of Green's relations in semigroups, particularly
in finite semigroups.

## Main statements

* `R_eqv_right_cancel`, `L_eqv_left_cancel`: Cancellation properties of ğ“¡ and ğ“› relations
* `exists_pow_sandwich_eq_self`: A key lemma in proofs on Finite Semigroups.
* `D_iff_J_finite`: The D-J theorem for finite semigroups
* `le_R_idempotent`, `le_L_idempotent`: Characterization of elements below idempotents
* `R_eqv_of_J_mul_right`, `L_eqv_of_J_mul_left`: Shows how ğ“™ equivilance "strengthens"
  ğ“¡ and ğ“› preorders to equivalences in finite semigroups.

## TODO
* Finish Greens Lemma
-/

/-! ### Simp Lemmas
This section provides lemmas tagged with @[simp]. For lemmas that take hypothesis
like `h : a â‰¤ğ“¡ a * b`, make sure that you call `simp [h]` to use them. -/

section Simp_Lemmas

variable {S} [Semigroup S] (a x y : S)

/-- `a * x` is always `â‰¤ğ“¡ a` -/
@[simp] lemma mul_right_R_preorder_self : a * x â‰¤ğ“¡ a := by
  use x; rw [WithOne.coe_mul]

/-- `x * a` is always `â‰¤ğ“› a` -/
@[simp] lemma mul_left_L_preorder_self : x * a â‰¤ğ“› a := by
  use x; rw [WithOne.coe_mul]

/-- `x * a * y` is always `â‰¤ğ“™ a` -/
@[simp] lemma mul_sandwich_J_preorder_self : x * a * y â‰¤ğ“™ a := by
  use x, y; simp

variable {a x y : S}

/-- `a â‰¤ğ“¡ a * x` implies `a ğ“¡ a * x` -/
@[simp] lemma R_eqv_of_R_preorder_mul_right (h : a â‰¤ğ“¡ a * x) : a ğ“¡ a * x := by
  simpa [R_eqv_iff]

/-- `a â‰¤ğ“› x * a` implies `a ğ“› b * a` -/
@[simp] lemma L_eqv_of_L_preorder_mul_left (h : a â‰¤ğ“› x * a) : a ğ“› x * a := by
  simpa [L_eqv_iff]

/-- `a â‰¤ğ“™ x * a * y` implies `a ğ“™ x * a * y` -/
@[simp] lemma J_eqv_of_J_preorder_mul_sandwich (h : a â‰¤ğ“™ x * a * y) : a ğ“™ x * a * y := by
  simpa [J_eqv_iff]

end Simp_Lemmas

/-! ### Lemmas for ğ“¡ and ğ“› equivalences (Prop 1.4.3)
This section establishes cancellation and extension properties for Green's ğ“¡ and ğ“› relations. -/

section RL_Lemmas

variable {S} [Semigroup S] {a x y : S}

/-! Lemmas assuming `a ğ“¡ axy` -/

/-- If `a ğ“¡ a * x * y`, then `a ğ“¡ a * x`. -/
lemma R_eqv_right_cancel (h : a ğ“¡ a * x * y) : a ğ“¡ a * x := by
  simp_all [R_eqv_iff]
  obtain âŸ¨âŸ¨u, huâŸ©, _âŸ© := h
  use y * u; simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  exact hu

/-- If `a ğ“¡ a * x * y`, then `a * x ğ“¡ a * x * y`.-/
lemma R_eqv_right_extend (h : a ğ“¡ a * x * y) : a * x ğ“¡ a * x * y := by
  simp_all [R_eqv_iff]
  obtain âŸ¨âŸ¨u, huâŸ©, _âŸ© := h
  use u * x; simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  rw [â† hu]

/-! Lemmas assuming `a ğ“› yxa` -/

/-- If `a ğ“› y * x * a`, then `a ğ“› x * a`. -/
theorem L_eqv_left_cancel (h: a ğ“› y * x * a ) : a ğ“› x * a := by
  simp_all [L_eqv_iff]
  obtain âŸ¨u, huâŸ© := h
  use u * y; simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  exact hu

/-- If `a ğ“› y * x * a`, then `x * a ğ“› y * x * a`. -/
theorem L_eqv_left_extend (h : a ğ“› y * x * a) : x * a ğ“› y * x * a := by
  simp_all [L_eqv_iff]
  constructor
  Â· obtain âŸ¨u, huâŸ© := h
    use x * u; simp_rw [WithOne.coe_mul, mul_assoc] at *
    rw [â† hu]
  Â· simp [mul_assoc]

end RL_Lemmas

/-! ### Lemmas for Monoids -/

section Monoid_Lemmas

variable {M} [Monoid M] {a x y : M}

/-- In finite monoids, if `a = x * a * y`, then there exist positive integers `nâ‚` and `nâ‚‚`
such that `x ^ nâ‚ * a = a` and `a * y ^ nâ‚‚ = a`. -/
lemma exists_pow_sandwich_eq_self [Finite M] (h : a = x * a * y) :
    âˆƒ nâ‚ nâ‚‚ : â„•, nâ‚ â‰  0 âˆ§ nâ‚‚ â‰  0 âˆ§ x ^ nâ‚ * a = a âˆ§ a * y ^ nâ‚‚ = a := by
  have loop : âˆ€ k : â„•, x ^ k * a * y ^ k = a := by
    intro k; induction k with
    | zero => simp
    | succ n ih =>
      rw [pow_succ, pow_succ']
      rw [â† mul_assoc, mul_assoc _ a, mul_assoc _ x, â† mul_assoc x a y, â† h, ih]
  have âŸ¨nâ‚, âŸ¨hnâ‚, hneqâ‚âŸ©âŸ© := Monoid.exists_idempotent_pow x
  have âŸ¨nâ‚‚, âŸ¨hnâ‚‚, hneqâ‚‚âŸ©âŸ© := Monoid.exists_idempotent_pow y
  use nâ‚, nâ‚‚
  constructor; exact hneqâ‚; constructor; exact hneqâ‚‚; constructor
  Â· rw [â† (loop nâ‚), â† mul_assoc, â† mul_assoc, hnâ‚]
  Â· rw [â† (loop nâ‚‚), mul_assoc, hnâ‚‚]

end Monoid_Lemmas

/-! ### The D-J Theorem for Finite Semigroups -/

section D_J

variable {S} [Semigroup S] {a b : S}

/-- Every ğ““-related pair is ğ“™-related. -/
lemma D_eqv_if : a ğ““ b â†’ a ğ“™ b := by
  simp [D_eqv_iff, J_eqv_iff]
  rintro x âŸ¨âŸ¨c, hcâŸ©, âŸ¨d, hdâŸ©âŸ© âŸ¨âŸ¨f, hfâŸ©, âŸ¨g, hgâŸ©âŸ©
  constructor
  Â· use f, c; rw [â† hf, â† hc]
  Â· use g, d; rw [mul_assoc, â† hd, hg]

/-- In finite semigroups, Green's ğ““-relation equals the ğ“™-relation. The forward direction
always holds, but finiteness is needed for the reverse implication. -/
theorem D_iff_J_finite [Finite S] : a ğ““ b â†” a ğ“™ b := by
  constructor
  Â· apply D_eqv_if
  Â· intro hJ
    have hJ_copy := hJ; obtain âŸ¨âŸ¨x, y, haâŸ©, âŸ¨u, v, hbâŸ©âŸ© := hJ_copy
    have hab : â†‘a = x * u * a * (v * y) := by
      rw [hb, â† mul_assoc, â† mul_assoc, mul_assoc _ v y] at ha
      exact ha
    obtain âŸ¨k, l, hene, hfne, heq1, heq2âŸ© := exists_pow_sandwich_eq_self hab
    cases v with
    | one =>
      use a; rw [one_mul, mul_one] at *; simp [L_eqv_iff]
      constructor
      Â· use (x * u)^(k-1) * x
        have hk : k - 1 + 1 = k := by exact Nat.succ_pred_eq_of_ne_zero hene
        rw [hb, â† mul_assoc, mul_assoc _ x u, â† pow_succ, hk, heq1]
      Â· use u
    | coe v =>
      use a * v
      simp [R_eqv_iff, L_eqv_iff]
      constructor
      Â· use y * (v * y) ^ (l - 1)
        rw [WithOne.coe_mul, â† mul_assoc, mul_assoc â†‘a â†‘v y, mul_assoc,  â† pow_succ']
        have hl : l - 1 + 1 = l := by exact Nat.succ_pred_eq_of_ne_zero hfne
        rw [hl, heq2]
      Â· constructor
        Â· use (x * u)^(k-1) * x; rw [hb]
          conv => rhs; rw [â† mul_assoc, â† mul_assoc, mul_assoc _ x u]
          have hk : k - 1 + 1 = k := by exact Nat.succ_pred_eq_of_ne_zero hene
          rw [WithOne.coe_mul, â† pow_succ, hk, heq1]
        Â· use u; simp [hb, mul_assoc]

end D_J

/-! ### Idempotent properties (Prop 1.4.1)
This section characterizes elements that are ğ“¡- below or ğ“›- below an idempotent. -/

section Idempotent_Props

variable {S} [Semigroup S] (a e : S)

/-- An element `a` is ğ“¡-below an idempotent `e` if and only if `a = e * a`. -/
theorem le_R_idempotent (h: IsIdempotentElem e) : (a â‰¤ğ“¡ e) â†” (a = e * a) := by
  constructor
  Â· rintro âŸ¨u, hruâŸ©
    unfold IsIdempotentElem at h
    rw [â† WithOne.coe_inj, WithOne.coe_mul] at h âŠ¢
    rw [hru, â† mul_assoc, h ]
  Â· intro hl; use a
    rw[<-WithOne.coe_inj] at hl; exact hl

/-- An element `a` is ğ“›-below an idempotent `e` if and only if `a = a * e`. -/
theorem le_L_idempotent (h: IsIdempotentElem e) : (a â‰¤ğ“› e) â†” (a = a * e) := by
  constructor
  Â· rintro âŸ¨u, hruâŸ©
    unfold IsIdempotentElem at h
    rw [â† WithOne.coe_inj, WithOne.coe_mul] at h âŠ¢
    rw [hru, mul_assoc, h ]
  Â· intro hl; use a
    rw[<-WithOne.coe_inj] at hl; exact hl

end Idempotent_Props

/-! ### Properties relating J, L, and R (Proposition 1.4.2 and 1.4.4)
This section shows how ğ“™-equivalence "strengthens"
ğ“¡ and ğ“› preorders to equivalences in finite semigroups. -/

section J_R_L_Props

variable {S} [Semigroup S] [Finite S] {a b : S}

/-- In finite semigroups, if `a` is ğ“™-related to `a * b`, then `a` is ğ“¡-related to `a * b`. -/
lemma R_eqv_of_J_mul_right (hj : a ğ“™ a * b) : a ğ“¡ a * b := by
  obtain âŸ¨âŸ¨x, y, hxyâŸ©, _âŸ© := hj
  rw [WithOne.coe_mul, â† mul_assoc, mul_assoc] at hxy
  simp [R_eqv_iff]
  obtain âŸ¨_, n, _, hneq, _, ha âŸ© := exists_pow_sandwich_eq_self hxy
  use y * (â†‘b * y) ^ (n - 1)
  simp_rw [WithOne.coe_mul, â† mul_assoc, mul_assoc â†‘a â†‘b y]
  rw [mul_assoc â†‘a (â†‘b * y), â† pow_succ']
  have hl : n - 1 + 1 = n := by exact Nat.succ_pred_eq_of_ne_zero hneq
  rw [hl, ha]

/-- In finite semigroups, if `a` is ğ“¡-below `b` and `a ğ“™ b`, then `a ğ“¡ b`. -/
theorem R_eqv_of_R_preorder_and_J (hr : a â‰¤ğ“¡ b) (hj: a ğ“™ b) : a ğ“¡ b := by
  rw [R_preorder_iff_without_one] at hr
  cases hr with
  | inl heq => subst heq; simp
  | inr ha =>
    obtain âŸ¨u, hruâŸ© := ha; subst hru;
    rw [J_eqv_symm, R_eqv_symm] at *
    apply R_eqv_of_J_mul_right; assumption

/-- In finite semigroups, if `a` is ğ“™-related to `b * a`, then `a` is ğ“›-related to `b * a`. -/
lemma L_eqv_of_J_mul_left (hj : a ğ“™ b * a) : a ğ“› b * a := by
  obtain âŸ¨âŸ¨x, y, hxyâŸ©, _âŸ© := hj
  rw [WithOne.coe_mul, â† mul_assoc ] at hxy
  simp [L_eqv_iff]
  obtain âŸ¨ n, _, hneq, _, ha, _ âŸ© := exists_pow_sandwich_eq_self hxy
  use (x * â†‘b) ^ (n - 1) * x
  simp_rw [WithOne.coe_mul, â† mul_assoc, mul_assoc _ x, â† pow_succ]
  have hl : n - 1 + 1 = n := by exact Nat.succ_pred_eq_of_ne_zero hneq
  rw [hl, ha]

/-- In finite semigroups, if `a` is ğ“›-below `b` and `a ğ“™ b`, then `a ğ“› b`. -/
theorem L_eqv_of_L_preorder_and_J (hl : a â‰¤ğ“› b) (hj: a ğ“™ b) : a ğ“› b := by
  rw [L_preorder_iff_without_one] at hl
  cases hl with
  | inl heq => subst heq; simp
  | inr ha =>
    obtain âŸ¨u, hruâŸ© := ha; subst hru;
    rw [J_eqv_symm, L_eqv_symm] at *
    apply L_eqv_of_J_mul_left; assumption

end J_R_L_Props

/-! ### Green's Lemma (Proposition 1.5)
This section demonstrates various results related to Green's theorem. We prove that
that left/right translations induced by witnesses to two ğ“¡/ğ“› related elements are
bijections between their L/R classes, respectively, that preserve H classes. -/

section Greens_Lemma

variable {S} [Semigroup S] {a b : S}

/-Necessary definitions-- to be moved-/
def R_translation (a : SÂ¹) : SÂ¹ â†’ SÂ¹ := (Â· * a)
notation:50 "Ïáµ£" a => R_translation a
infixr:70 " â‹†Ïáµ£ " => R_translation

def L_translation (a : SÂ¹) : SÂ¹ â†’ SÂ¹ := (a * Â·)
notation:50 "Ïâ‚—" a => L_translation a
infixr:70 " â‹†Ïâ‚— " => L_translation

def R_class_set (x : SÂ¹) : Set (SÂ¹) :=
  {a | a ğ“¡ x}
def L_class_set (x : SÂ¹) : Set (SÂ¹) :=
  { a | a ğ“› x}

/-Helper lemmas: -/

/-If a ğ“¡ b such that b = a * u and a = b * v, then translation by (u * v) on
any x such that xğ“›a is the identity. -/
lemma right_translation_id_ab (u v : SÂ¹) (h : a ğ“¡ b) (hu : b = a * u) (hv : a = b * v) :
    âˆ€x âˆˆ (L_class_set â†‘a), (v â‹†Ïáµ£ (u â‹†Ïáµ£ x)) = x := by
    intros c hc
    have hac : â†‘a ğ“› c := by exact L_eqv_symm.mp hc
    have ht : âˆƒ x, c = x * a := by
      refine (L_preorder_iff_monoid c â†‘a).mp ?_; exact hac.right
    obtain âŸ¨t, ht'âŸ© := ht
    have : (v â‹†Ïáµ£ ( u â‹†Ïáµ£ c)) = c := by
      calc
      (v â‹†Ïáµ£ ( uâ‹†Ïáµ£ c)) = (v â‹†Ïáµ£ ( u â‹†Ïáµ£ c)) := by rfl
      _ = c * u * v := by unfold R_translation; rfl
      _ = t * b * v := by simp_rw[ht', hu, mul_assoc]
      _ = t * a := by rw[hv, mul_assoc]
      _ = c := by rw[ht']
    exact this

/-If a ğ“¡ b such that b = a * u and a = b * v, then translation by (v * u) on
any x such that xğ“›b is the identity. -/
lemma right_translation_id_ba (u v : SÂ¹) (h : a ğ“¡ b) (hu : b = a * u) (hv : a = b * v) :
    âˆ€x âˆˆ (L_class_set â†‘b), (u â‹†Ïáµ£ (v â‹†Ïáµ£ x)) = x := by
    intros c hc
    have hac : â†‘b ğ“› c := by exact L_eqv_symm.mp hc
    have ht : âˆƒ x, c = x * b := by
      refine (L_preorder_iff_monoid c â†‘b).mp ?_; exact hac.right
    obtain âŸ¨t, ht'âŸ© := ht
    have : (u â‹†Ïáµ£ (v â‹†Ïáµ£ c)) = c := by
      calc
      (u â‹†Ïáµ£ (v â‹†Ïáµ£ c)) = (u â‹†Ïáµ£ (v â‹†Ïáµ£ c)) := by rfl
      _ = c * v * u := by unfold R_translation; rfl
      _ = t * a * u := by simp_rw[ht', hv, mul_assoc]
      _ = t * b := by rw[hu, mul_assoc]
      _ = c := by rw[ht']
    exact this
/-end of helper lemmas.-/

/-If a ğ“¡ b such that b = a * u and a = b * v, then right translation by
(v * u) from the ğ“›-class of a to the ğ“›-class of b
and by (u * v) ğ“›-class of b to the ğ“›-class of are inverses.-/
theorem Greens_lemma_inverse_bijections_r_trans {u v : SÂ¹}
    (h: a ğ“¡ b) (hv : a = b * v) (hu : b = a * u) :
    Set.InvOn (Ïáµ£ v) (Ïáµ£ u) (L_class_set a) (L_class_set b) := by
    unfold Set.InvOn
    exact âŸ¨right_translation_id_ab u v h hu hv, right_translation_id_ba u v h hu hvâŸ©

/-If a ğ“¡ b such that b = a * u, right translation by u is ğ“›-class of a is a
bijection on the ğ“›-class of b.-/
theorem Greens_lemma_R_rel_ab {u : SÂ¹} (h: a ğ“¡ b) (hu: b = a * u):
    (Set.BijOn (Ïáµ£ u) (L_class_set a) (L_class_set b)) := by
  have h' := h
  unfold R_eqv at h; unfold eqv_of_preorder at h
  obtain âŸ¨âŸ¨v, hvâŸ©, _âŸ© := h
  refine Set.BijOn.mk ?_ ?_ ?_
  Â· intros x hx
    have hxa : x ğ“› a := hx
    have : x * u ğ“› a * u := âŸ¨L_preorder_rmult_compat hxa.left u,
      L_preorder_rmult_compat hxa.right uâŸ©
    rw [â†hu] at this
    exact this
  Â· intro x hx y hy hxy
    have hinv : âˆ€x âˆˆ (L_class_set â†‘a), (v â‹†Ïáµ£ (u â‹†Ïáµ£ x)) = x
      := by exact fun x a_1 â†¦ right_translation_id_ab u v h' hu hv x a_1
    have hinvx := hinv x hx; have hinvy := hinv y hy
    rw[<-hinvx, <-hinvy, hxy]
  Â· intros y hy
    refine (Set.mem_image (Ïáµ£ u) (L_class_set â†‘a) y).mpr ?_
    let x := y * v
    have hx : x âˆˆ L_class_set a := by
      have hyb : y ğ“› b := hy
      have h1 : x ğ“› b * v := âŸ¨L_preorder_rmult_compat hyb.left v,
                                L_preorder_rmult_compat hyb.right vâŸ©
      rw [â†hv] at h1; exact h1
    use x, hx
    have hinv : âˆ€y âˆˆ (L_class_set â†‘b), (u â‹†Ïáµ£ (v â‹†Ïáµ£ y)) = y
     := by exact fun y a_1 â†¦ right_translation_id_ba u v h' hu hv y a_1
    have hinvx := hinv y hy; unfold R_translation at hinvx
    calc
       x * u = y * v * u := rfl
       _ = y := by rw[hinvx]

/- If a ğ“¡ b such that b = a * u, right translation by u preserves ğ“—-classes.-/
theorem Greens_lemma_r_trans_preserves_H_ab {x y u v: SÂ¹} (h: a ğ“¡ b) (h1: b = a * u)
    (h2: a = b * v)
    (hx : x âˆˆ L_class_set â†‘a) (hy : y âˆˆ L_class_set â†‘a):
    (x ğ“— y) â†” (u â‹†Ïáµ£ x) ğ“— (u â‹†Ïáµ£ y) := by
  constructor
  Â· intro hxy
    refine (H_eqv_iff_L_and_R (x * u) (y * u)).mpr ?_
    have : x ğ“¡ y âˆ§ x ğ“› y := by simp[H_eqv_iff_L_and_R x y] at hxy; exact hxy
    obtain âŸ¨hxyr, hxylâŸ© := this
    constructor
    Â· have hxyr' := hxyr
      unfold R_eqv at hxyr âŠ¢
      obtain âŸ¨âŸ¨r1, hr1âŸ©, âŸ¨r2, hr2âŸ©âŸ© := hxyr
      have hu : âˆ€x, (x âˆˆ (L_class_set â†‘a)) â†’ x ğ“¡ x * u := by
        unfold R_eqv; unfold eqv_of_preorder
        simp_rw[R_preorder_iff_without_one]
        have hinv : âˆ€x âˆˆ (L_class_set â†‘a), (v â‹†Ïáµ£ (u â‹†Ïáµ£ x)) = x
          := by exact fun x a_1 â†¦ right_translation_id_ab u v h h1 h2 x a_1
        intros x hx
        have hinvx := hinv x hx; unfold R_translation at hinvx
        constructor
        Â· refine Or.symm (Or.intro_left (x = x * u) ?_); use v; exact hinvx.symm
        Â· refine Or.symm (Or.intro_left (x * u = x) ?_); use u
      have hxu := hu x hx
      have hyu := hu y hy
      constructor
      Â· have : x * u â‰¤ğ“¡ y := by
          refine R_preorder_trans (x * u) x y ?_ ?_
          exact hxu.right; exact hxyr'.left
        have : x * u â‰¤ğ“¡ y * u := by
          refine R_preorder_trans (x * u) y (y * u) ?_ ?_
          exact this; exact hyu.left
        exact this
      Â· have : y * u â‰¤ğ“¡ x := by
          refine R_preorder_trans (y * u) y x ?_ ?_
          exact hyu.right; exact hxyr'.right
        have :  y * u â‰¤ğ“¡ x * u := by
          refine R_preorder_trans (y * u) x (x * u) ?_ ?_
          exact this; exact hxu.left
        exact this
    Â· exact L_eqv_rmult_compat hxyl u
  Â· intro hxyu; unfold R_translation at hxyu
    have hv : âˆ€x, (x âˆˆ (L_class_set â†‘b)) â†’ x ğ“¡ x * v := by
      unfold R_eqv; unfold eqv_of_preorder
      simp_rw[R_preorder_iff_without_one]
      have hinv : âˆ€x âˆˆ (L_class_set â†‘b), (u â‹†Ïáµ£ (v â‹†Ïáµ£ x)) = x
        := by exact fun x a_1 â†¦ right_translation_id_ba u v h h1 h2 x a_1
      intros x hx
      have hinvx := hinv x hx; unfold R_translation at hinvx
      constructor
      Â· refine Or.symm (Or.intro_left (x = x * v) ?_); use u; exact hinvx.symm
      Â· refine Or.symm (Or.intro_left (x * v = x) ?_); use v
    have hxu :(x * u) âˆˆ L_class_set â†‘b := by
      have hxa : x ğ“› a := hx
      have := L_eqv_rmult_compat hxa u
      rw[<-h1] at this; exact this
    have hyu :(y * u) âˆˆ L_class_set â†‘b := by
      have hya : y ğ“› a := hy
      have := L_eqv_rmult_compat hya u
      rw[<-h1] at this; exact this
    have hxv := hv (x * u) (hxu : (x * u) âˆˆ L_class_set â†‘b)
    have hxy := hv (y * u) (hyu : (y * u) âˆˆ L_class_set â†‘b)
    have x_eq : (x * u) * v = x := right_translation_id_ab u v h h1 h2 x hx
    have y_eq : (y * u) * v = y := right_translation_id_ab u v h h1 h2 y hy
    have hloopr : x * u * v ğ“¡ y * u * v := by
      constructor
      Â· have h1: x * u * v â‰¤ğ“¡ x * u := by
          simp_rw[R_preorder_iff_without_one]
          refine Or.symm (Or.intro_left (x * u * v = x * u) ?_); use v
        have h2: x * u â‰¤ğ“¡ y * u:= hxyu.left.left
        have h3: y * u â‰¤ğ“¡ y * u * v := by
          simp_rw[R_preorder_iff_without_one]
          rw[y_eq]
          refine Or.symm (Or.intro_left (y * u = y) ?_); use u
        have : x * u * v â‰¤ğ“¡ y * u := by
          refine R_preorder_trans (x * u * v) (x * u) (y * u) ?_ ?_
          exact h1; exact h2
        refine R_preorder_trans (x * u * v) (y * u) (y * u * v) ?_ ?_
        exact this; exact h3
      Â· have h1: y * u * v â‰¤ğ“¡ y * u := by
          simp_rw[R_preorder_iff_without_one]
          refine Or.symm (Or.intro_left (y * u * v = y * u) ?_); use v
        have h2: y * u â‰¤ğ“¡ x * u:= hxyu.right.left
        have h3: x * u â‰¤ğ“¡ x * u * v := by
          simp_rw[R_preorder_iff_without_one]
          rw[x_eq]
          refine Or.symm (Or.intro_left (x * u = x) ?_); use u
        have : y * u * v â‰¤ğ“¡ x * u := by
          refine R_preorder_trans (y * u * v) (y * u) (x * u) ?_ ?_
          exact h1; exact h2
        refine R_preorder_trans (y * u * v) (x * u) (x * u * v) ?_ ?_
        exact this; exact h3
    rw[x_eq, y_eq] at hloopr
    have hloopl : x * u * v ğ“› y * u * v := âŸ¨L_preorder_rmult_compat hxyu.left.right v,
      L_preorder_rmult_compat hxyu.right.right vâŸ©
    rw[x_eq, y_eq] at hloopl
    have:= H_eqv_iff_L_and_R x y
    exact (H_eqv_iff_L_and_R x y).mpr âŸ¨hloopr, hlooplâŸ©
