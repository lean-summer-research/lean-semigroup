import MyProject.Idempotence

/-!
# Green's Relations Definitions

This file defines Green's relations (ğ“¡, ğ“›, ğ“™, and ğ“—) for semigroups.

## Main Definitions

**Preorder Relations**:
  * `R_preorder a b` - The ğ“¡ preorder: a â‰¤ğ“¡ b iff a*SÂ¹ âŠ† b*SÂ¹
  * `L_preorder a b` - The ğ“› preorder: a â‰¤ğ“› b iff SÂ¹*a âŠ† SÂ¹*b
  * `J_preorder a b` - The ğ“™ preorder: a â‰¤ğ“™ b iff SÂ¹*a*SÂ¹ âŠ† SÂ¹*b*SÂ¹
  * `H_preorder a b` - The ğ“— preorder: intersection of ğ“¡ and ğ“›

**Equivalence Relations**:
  * `R_eqv`, `L_eqv`, `J_eqv`, `H_eqv` - Green's equivalence relations
  * `R_class S`, `L_class S`, etc. - Quotient types for equivalence classes

**Alternative Characterizations**:
  * Coset-based definitions showing equivalences via principal ideals
  * Monoid-specific versions eliminating the need for SÂ¹

## Notation

* **Preorders**: `a â‰¤ğ“¡ b`, `a â‰¤ğ“› b`, `a â‰¤ğ“™ b`, `a â‰¤ğ“— b`
* **Equivalences**: `a ğ“¡ b`, `a ğ“› b`, `a ğ“™ b`, `a ğ“— b`
* **Classes**: `âŸ¦aâŸ§ğ“¡`, `âŸ¦aâŸ§ğ“›`, `âŸ¦aâŸ§ğ“™`, `âŸ¦aâŸ§ğ“—`
* **Cosets**: `M â€¢ a`, `a â€¢ M`, `M â€¢â€¢ a`

## Implementation Notes

This file imports `Idempotence.lean` and is imported by `Decidable.lean`. It provides the
foundational definitions for Green's relations, with computational instances added in the
`GreensRelations.Decidable.lean`
-/

/-! ### Equivalence of Preorder

This section establishes the general framework for converting any preorder into an equivalence
relation. Given a preorder `p`, we define `eqv_of_preorder p a b` as `p a b âˆ§ p b a`, and we
instantiate it with the `Equivalence` type-class.
-/

section Equivalence_of_Preorder

variable {Î±} (p : Î± â†’ Î± â†’ Prop) [inst : IsPreorder Î± p]

@[simp] def eqv_of_preorder (a b : Î±) : Prop := p a b âˆ§ p b a

@[refl] lemma eqv_of_preorder_refl (a : Î±) : eqv_of_preorder p a a := by simp; apply inst.refl

set_option linter.unusedSectionVars false in
@[symm] lemma eqv_of_preorder_symm {a b : Î±} : eqv_of_preorder p a b â†’ eqv_of_preorder p b a := by aesop

@[trans] lemma eqv_of_preorder_trans {a b c : Î±} :
    eqv_of_preorder p a b â†’ eqv_of_preorder p b c â†’ eqv_of_preorder p a c:= by
  simp; intros Hâ‚ Hâ‚‚ Hâ‚ƒ Hâ‚„
  constructor
  Â· exact (inst.trans a b c Hâ‚ Hâ‚ƒ)
  Â· exact (inst.trans c b a Hâ‚„ Hâ‚‚)

-- Equivalence is the unbundled form of Setoid
instance eqv_of_preorder_inst : Equivalence (eqv_of_preorder p) where
  refl := eqv_of_preorder_refl p
  symm := eqv_of_preorder_symm p
  trans := eqv_of_preorder_trans p

end Equivalence_of_Preorder

/-! ### Greens Relations Definitions

This section defines the four Green's preorders (ğ“¡, ğ“›, ğ“™, ğ“—) for semigroups using the monoid
extension SÂ¹. Each preorder is shown to be reflexive and transitive, with corresponding
equivalence relations derived using the framework from the previous section.
-/

section Definitions

variable {S} [Semigroup S]

/-- the ğ“¡ preorder: a â‰¤ğ“¡ b iff a*SÂ¹ âŠ† b*SÂ¹ -/
def R_preorder (a b : S) : Prop := âˆƒ x : SÂ¹ , â†‘a = â†‘b * x
/-- the ğ“› preorder: a â‰¤ğ“› b iff SÂ¹*a âŠ† SÂ¹*b -/
def L_preorder (a b : S) : Prop := âˆƒ x : SÂ¹, â†‘a = x * â†‘b
/-- the ğ“™ preorder: a â‰¤ğ“™ b iff SÂ¹*a*SÂ¹ âŠ† SÂ¹*b*SÂ¹ -/
def J_preorder (a b : S) : Prop := âˆƒ x y : SÂ¹, a = x * b * y
/-- the ğ“— preorder -/
def H_preorder (a b : S) : Prop := R_preorder a b âˆ§ L_preorder a b

/-! Preorder notation, typed \leq\MCR -/
notation:50 f " â‰¤ğ“¡ " g:50 => R_preorder f g
notation:50 f " â‰¤ğ“› " g:50 => L_preorder f g
notation:50 f " â‰¤ğ“™ " g:50 => J_preorder f g
notation:50 f " â‰¤ğ“— " g:50 => H_preorder f g

/-! Reflexivity lemmas -/
lemma R_preorder_refl (x : S) : x â‰¤ğ“¡ x := by use 1; simp
lemma L_preorder_refl (x : S) : x â‰¤ğ“› x := by use 1; simp
lemma J_preorder_refl (x : S) : x â‰¤ğ“™ x := by use 1, 1; simp
lemma H_preorder_refl (x : S) : x â‰¤ğ“— x := by
  unfold H_preorder; apply And.intro <;> use 1; simp; rw [one_mul]

/-! Transitivity lemmas -/
lemma R_preorder_trans (a b c : S) : a â‰¤ğ“¡ b â†’ b â‰¤ğ“¡ c â†’ a â‰¤ğ“¡ c := by
  simp [R_preorder]; intros hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
  use (hâ‚ƒ * hâ‚)
  rw [â† mul_assoc, â† hâ‚„, hâ‚‚]
lemma L_preorder_trans (a b c : S) : a â‰¤ğ“› b â†’ b â‰¤ğ“› c â†’ a â‰¤ğ“› c := by
  unfold L_preorder; rintro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©
  use x * y; rw [hx, hy, mul_assoc]
lemma J_preorder_trans (a b c : S) : a â‰¤ğ“™ b â†’ b â‰¤ğ“™ c â†’ a â‰¤ğ“™ c := by
  unfold J_preorder; rintro âŸ¨xâ‚, yâ‚, hxâŸ© âŸ¨xâ‚‚, yâ‚‚, hyâŸ©
  use xâ‚ * xâ‚‚, yâ‚‚ * yâ‚; rw [hx, hy]; simp [mul_assoc]
lemma H_preorder_trans (a b c : S) : a â‰¤ğ“— b â†’ b â‰¤ğ“— c â†’ a â‰¤ğ“— c := by
  unfold H_preorder; rintro âŸ¨hâ‚, hâ‚‚âŸ© âŸ¨hâ‚ƒ, hâ‚„âŸ©
  constructor
  Â· apply R_preorder_trans a b c <;> assumption
  Â· apply L_preorder_trans a b c <;> assumption

/-! Preorder Instances -/
instance R_is_preorder : IsPreorder S R_preorder where
  refl := R_preorder_refl
  trans := R_preorder_trans
instance L_is_preorder : IsPreorder S L_preorder where
  refl := L_preorder_refl
  trans := L_preorder_trans
instance J_is_preorder : IsPreorder S J_preorder where
  refl := J_preorder_refl
  trans := J_preorder_trans
instance H_is_preorder : IsPreorder S H_preorder where
  refl := H_preorder_refl
  trans := H_preorder_trans

/-- The equivalence relation `ğ“¡` -/
def R_eqv : S â†’ S â†’ Prop := eqv_of_preorder R_preorder
/-- The equivalence relation `ğ“›` -/
def L_eqv : S â†’ S â†’ Prop := eqv_of_preorder L_preorder
/-- The equivalence relation `ğ“™` -/
def J_eqv : S â†’ S â†’ Prop := eqv_of_preorder J_preorder
/-- The equivalence relation `ğ“—` -/
def H_eqv : S â†’ S â†’ Prop := eqv_of_preorder H_preorder

/-! Equivalence notation: typed \MCR -/
notation:50 a " ğ“¡ " b:50 => R_eqv a b
notation:50 a " ğ“› " b:50 => L_eqv a b
notation:50 a " ğ“™ " b:50 => J_eqv a b
notation:50 a " ğ“— " b:50 => H_eqv a b

/-! Definitional lemmas for equivilance relations -/
lemma R_eqv_iff (a b : S) : a ğ“¡ b â†” a â‰¤ğ“¡ b âˆ§ b â‰¤ğ“¡ a := by unfold R_eqv; simp
lemma L_eqv_iff (a b : S) : a ğ“› b â†” a â‰¤ğ“› b âˆ§ b â‰¤ğ“› a := by unfold L_eqv; simp
lemma J_eqv_iff (a b : S) : a ğ“™ b â†” a â‰¤ğ“™ b âˆ§ b â‰¤ğ“™ a := by unfold J_eqv; simp
lemma H_eqv_iff (a b : S) : a ğ“— b â†” a â‰¤ğ“— b âˆ§ b â‰¤ğ“— a := by unfold H_eqv; simp

end Definitions

section Quot

/-! ### The type of the equivalence classes as a `Quot`

This section constructs quotient types for Green's equivalence classes using Lean's `Quot`
constructor. It provides functions to map elements to their equivalence classes and establishes
the relationship between quotient equality and the underlying equivalence relations.
-/

def R_class (S) [Semigroup S] := Quot (fun a b : S => a ğ“¡ b)
def L_class (S) [Semigroup S] := Quot (fun a b : S => a ğ“› b)
def J_class (S) [Semigroup S] := Quot (fun a b : S => a ğ“™ b)
def H_class (S) [Semigroup S] := Quot (fun a b : S => a ğ“— b)

variable {S} [Semigroup S]

/-! Functions from elements of `S` to their equivalence classes -/
def get_R_class (a : S) : R_class S := Quot.mk R_eqv a
def get_L_class (a : S) : L_class S := Quot.mk L_eqv a
def get_J_class (a : S) : J_class S := Quot.mk J_eqv a
def get_H_class (a : S) : H_class S := Quot.mk H_eqv a

/-! Notation for the functions returning equivalence classes of `S` -/
notation:50 "âŸ¦"x"âŸ§ğ“¡" => get_R_class x
notation:50 "âŸ¦"x"âŸ§ğ“›" => get_L_class x
notation:50 "âŸ¦"x"âŸ§ğ“™" => get_J_class x
notation:50 "âŸ¦"x"âŸ§ğ“—" => get_H_class x

/-! Unlike `Quotient r`, `Quot r` does not require a `Setoid` structure on the binary relation `r`.
In fact, it does not even require that `r` be reflexive, transitive, or symmetric. Instead, it
applies the function `Relation.EqvGen` to `r`, which modifies `r` to relate all elements necessary
to fulfill the setoid conditions. Our relations are already Refl, trans, and symm so this function
does not change them, which we prove here. -/
@[simp] lemma R_EqvGen_eq (a b : S): Relation.EqvGen R_eqv a b â†” a ğ“¡ b :=
  Equivalence.eqvGen_iff (eqv_of_preorder_inst R_preorder)
@[simp] lemma L_EqvGen_eq (a b : S): Relation.EqvGen L_eqv a b â†” a ğ“› b :=
  Equivalence.eqvGen_iff (eqv_of_preorder_inst L_preorder)
@[simp] lemma J_EqvGen_eq (a b : S): Relation.EqvGen J_eqv a b â†” a ğ“™ b :=
  Equivalence.eqvGen_iff (eqv_of_preorder_inst J_preorder)
@[simp] lemma H_EqvGen_eq (a b : S): Relation.EqvGen H_eqv a b â†” a ğ“— b :=
  Equivalence.eqvGen_iff (eqv_of_preorder_inst H_preorder)

/-! Definitional lemmas for equivilance classes -/
lemma R_class_iff (a b : S) : (âŸ¦aâŸ§ğ“¡ : R_class S) = (âŸ¦bâŸ§ğ“¡ : R_class S) â†” a ğ“¡ b := by
  unfold get_R_class
  rw [Quot.eq]; simp --simp finds R_EqvGen_eq
lemma L_class_iff (a b : S) : (âŸ¦aâŸ§ğ“› : L_class S) = (âŸ¦bâŸ§ğ“› : L_class S) â†” a ğ“› b := by
  unfold get_L_class
  rw [Quot.eq]; simp
lemma J_class_iff (a b : S) : (âŸ¦aâŸ§ğ“™ : J_class S) = (âŸ¦bâŸ§ğ“™ : J_class S) â†” a ğ“™ b := by
  unfold get_J_class
  rw [Quot.eq]; simp
lemma H_class_iff (a b : S) : (âŸ¦aâŸ§ğ“— : H_class S) = (âŸ¦bâŸ§ğ“— : H_class S) â†” a ğ“— b := by
  unfold get_H_class
  rw [Quot.eq]; simp

end Quot

/-! ### Alternate Definitions of Preorders

This section provides alternative characterizations of Green's preorders that avoid explicit
use of SÂ¹. It includes simplified definitions for semigroups, specialized versions for monoids,
and coset-based formulations that express the preorders in terms of principal ideal containment.
-/

section WithoutOne

variable {S} [Semigroup S] (a b : S)

theorem R_preorder_iff : a â‰¤ğ“¡ b â†” a = b âˆ¨ âˆƒ x, a = b * x := by
  unfold R_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => left; simp_all
    | coe y => right; use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro h; cases h with
    | inl h => use (1 : SÂ¹); simp_all
    | inr h => obtain âŸ¨x, hxâŸ© := h; use x; simp [â† WithOne.coe_mul, hx]

theorem L_preorder_iff  : a â‰¤ğ“› b â†” a = b âˆ¨ âˆƒ x, a = x * b := by
  unfold L_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => left; simp_all
    | coe y => right; use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro h; cases h with
    | inl h => use (1 : SÂ¹); simp_all
    | inr h => obtain âŸ¨x, hxâŸ© := h; use x; simp [â† WithOne.coe_mul, hx]

theorem J_preorder_iff : a â‰¤ğ“™ b â†” a = b âˆ¨ a â‰¤ğ“› b âˆ¨ a â‰¤ğ“¡ b âˆ¨ âˆƒ x y, a = x * b * y := by
  constructor
  Â· intro hJ; obtain âŸ¨x, y, hxyâŸ© := hJ; cases' x with x' <;> cases' y with y'
    Â· left; simp_all
    Â· right; right; left; use â†‘y'; exact hxy
    Â· right; left; use â†‘x'; exact hxy
    Â· right; right; right; use x', y'; simp [â† WithOne.coe_inj, hxy]
  Â· rintro (h | h | h | âŸ¨x, y, hxyâŸ©)
    Â· rw [h]; exact J_preorder_refl b
    Â· obtain âŸ¨x', hxâŸ© := h; use x', 1; simp [hx]
    Â· obtain âŸ¨y', hyâŸ© := h; use 1, y'; simp [hy]
    Â· use â†‘x, â†‘y; simp [hxy, WithOne.coe_mul]

theorem H_preorder_iff (a b : S) : a â‰¤ğ“— b â†” a â‰¤ğ“¡ b âˆ§ a â‰¤ğ“› b := by simp [H_preorder]

end WithoutOne

/-! Preorder definitions on Monoids, showing how MÂ¹ behaves like M-/

section MonoidPreorders

variable {M} [Monoid M] (a b : M)
theorem R_preorder_iff_monoid : a â‰¤ğ“¡ b â†” âˆƒ x, a = b * x := by
  unfold R_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => use 1; simp [â† WithOne.coe_inj, hx]
    | coe y => use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro âŸ¨x, hxâŸ©; use (â†‘x : MÂ¹); simp [â† WithOne.coe_inj, hx]

theorem L_preorder_iff_monoid : a â‰¤ğ“› b â†” âˆƒ x, a = x * b := by
  unfold L_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => use 1; simp [â† WithOne.coe_inj, hx]
    | coe y => use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro âŸ¨x, hxâŸ©; use (â†‘x : MÂ¹)
    rw [â† WithOne.coe_inj, WithOne.coe_mul] at hx; exact hx

theorem J_preorder_iff_monoid : a â‰¤ğ“™ b â†” âˆƒ x y, a = x * b * y := by
  unfold J_preorder; constructor
  Â· rintro âŸ¨x, y, hxyâŸ©; cases' x with x' <;> cases' y with y'
    Â· use 1, 1; simp_all
    Â· use 1, y'; simp [â† WithOne.coe_inj, hxy]
    Â· use x', 1; simp [â† WithOne.coe_inj, hxy]
    Â· use x', y'; simp [â† WithOne.coe_inj, hxy]
  Â· rintro âŸ¨x, y, hxyâŸ©; use (â†‘x : MÂ¹), (â†‘y : MÂ¹); simp [â† WithOne.coe_inj, hxy]

end MonoidPreorders

/-! Cosets as Sets -/

section Cosets

/-- the right coset `M â€¢ a` -/
def right_coset (M) [Monoid M] (a : M) : Set (M) := {x | âˆƒ y, x = y * a}
/-- the right coset `a â€¢ M` -/
def left_coset (M) [Monoid M] (a : M) : Set (M) := {x | âˆƒ y, x = a * y}
/-- the two_sided coset ` M â€¢ a â€¢ M` -/
def two_sided_coset (M) [Monoid M] (a : M) : Set (M) := {x | âˆƒ y z, x = y * a * z}

/-! Coset notation, typed \bub -/
notation:65 M " â€¢ " a:66 => right_coset M a
notation:65 a:66 " â€¢ " M => left_coset M a
notation:65 M " â€¢â€¢ " a:66  => two_sided_coset M a

variable {S} [Semigroup S] (a b : S)

/-! Coset lemmas -/
lemma right_coset_subset : (SÂ¹ â€¢ (a * b)) âŠ† (SÂ¹ â€¢ b) := by
  simp [right_coset]; intro x; use (x * a); simp [mul_assoc]
lemma left_coset_subset : ((a * b) â€¢ SÂ¹) âŠ† (a â€¢ SÂ¹) := by
  simp [left_coset]; intro x; use (b * x); simp [mul_assoc]

/-! Preorder Defs from Cosets -/

theorem R_preorder_iff_coset : a â‰¤ğ“¡ b â†” (a â€¢ SÂ¹) âŠ† (b â€¢ SÂ¹) := by
  rw [R_preorder_iff]
  constructor
  Â· intro h; cases h with
    | inl heq => simp [heq]
    | inr h => obtain âŸ¨x, hxâŸ© := h; rw [hx]; simp [left_coset_subset]
  Â· intro h; simp_all [left_coset]
    specialize h â†‘a 1; simp_all [â† R_preorder_iff, R_preorder]

theorem L_preorder_iff_coset : a â‰¤ğ“› b â†” (SÂ¹ â€¢ a) âŠ† (SÂ¹ â€¢ b) := by
  rw [L_preorder_iff]
  constructor
  Â· intro h; cases h with
    | inl heq => simp [heq]
    | inr h => obtain âŸ¨x, hxâŸ© := h; rw [hx]; simp [right_coset_subset]
  Â· intro h; simp_all [right_coset]
    specialize h â†‘a 1; simp_all [â† L_preorder_iff, L_preorder]

theorem J_preorder_iff_coset : a â‰¤ğ“™ b â†” (SÂ¹ â€¢â€¢ a) âŠ† (SÂ¹ â€¢â€¢ b) := by
  constructor
  Â· simp [J_preorder, two_sided_coset]
    rintro x y Hreach z t u Hz; subst Hz
    use (t * x), (y * u); simp_all [mul_assoc]
  Â· simp [J_preorder, two_sided_coset]
    intros H
    specialize H â†‘a 1 1; simp_all

theorem H_preorder_iff_coset : a â‰¤ğ“— b â†” (a â€¢ SÂ¹) âŠ† (b â€¢ SÂ¹) âˆ§ (SÂ¹ â€¢ a) âŠ† (SÂ¹ â€¢ b) := by
  simp [H_preorder, R_preorder_iff_coset, L_preorder_iff_coset]

/-! Equiv Relation Defs from Cosets -/

theorem R_eqv_iff_coset : a ğ“¡ b â†” (a â€¢ SÂ¹) = (b â€¢ SÂ¹) := by
  simp [R_eqv_iff, R_preorder_iff_coset, antisymm_iff]

theorem L_eqv_iff_coset : a ğ“› b â†” (SÂ¹ â€¢ a) = (SÂ¹ â€¢ b) := by
  simp [L_eqv_iff, L_preorder_iff_coset, antisymm_iff]

theorem J_eqv_iff_coset : a ğ“™ b â†” (SÂ¹ â€¢â€¢ a) = (SÂ¹ â€¢â€¢ b) := by
  simp [J_eqv_iff, J_preorder_iff_coset, antisymm_iff]

theorem H_eqv_iff_coset : a ğ“— b â†” a ğ“¡ b âˆ§ a ğ“› b := by
  simp [H_eqv, H_preorder_iff]
  rw [@and_comm (b â‰¤ğ“¡ a), and_assoc, â† @and_assoc _ _ (b â‰¤ğ“¡ a)]
  rw [ â† L_eqv_iff, and_comm, and_assoc, @and_comm (b â‰¤ğ“¡ a) _, â†  R_eqv_iff, and_comm]

end Cosets
