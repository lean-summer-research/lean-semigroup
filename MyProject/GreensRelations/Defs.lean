import MyProject.Idempotence

/-!
# Green's Relations Definitions

This file defines Green's relations ğ“¡, ğ“›, ğ“™, ğ“—, and ğ““ for semigroups.

## Main definitions

**Preorder Relations**:
* `R_preorder` - the ğ“¡ preorder: `a â‰¤ğ“¡ b` iff `a*SÂ¹ âŠ† b*SÂ¹`
* `L_preorder` - the ğ“› preorder: `a â‰¤ğ“› b` iff `SÂ¹*a âŠ† SÂ¹*b`
* `J_preorder` - the ğ“™ preorder: `a â‰¤ğ“™ b` iff `SÂ¹*a*SÂ¹ âŠ† SÂ¹*b*SÂ¹`
* `H_preorder` - the ğ“— preorder: intersection of ğ“¡ and ğ“›

**Equivalence Relations**:
* `R_eqv`, `L_eqv`, `J_eqv`, `H_eqv` - Green's equivalence relations
* `D_eqv` - the ğ““ relation defined as composition of ğ“¡ and ğ“›

**Principal Ideals**:
* `left_ideal`, `right_ideal`, `two_sided_ideal` - principal ideal constructions

## Notations

* **Preorders**: `a â‰¤ğ“¡ b`, `a â‰¤ğ“› b`, `a â‰¤ğ“™ b`, `a â‰¤ğ“— b`
* **Equivalences**: `a ğ“¡ b`, `a ğ“› b`, `a ğ“™ b`, `a ğ“— b`, `a ğ““ b`
* **Ideals**: `M â€¢ a` (left), `a â€¢ M` (right), `M â€¢â€¢ a` (two-sided)
* **Relation composition**: `r âˆ˜á´¿ s`

## TODO

* I started writing a ğ““ preoreder but I had trouble relating the equivilance it generates
through `eqv_of_preorder` to the definitnion as the composition of ğ“› and ğ“¡. I also could
not find anyone talking about a ğ““ preorder in the literature, and I'm wondering if it is
it even possible to define. It seemed like it was equivilant to the ğ“™ preorder.

* Can the alternate definitions of the preorders in monoids be more generalized? Should we
put that in withone.lean? Do all statments in `SÂ¹` hold in `S` and vice versa if `S` is
already a monoid?

-/

/-! ### Equivalence relations from preorders

This section establishes a general framework for converting preorders into equivalence
relations by taking the symmetric closure. This construction will be applied to Green's
preorders to obtain the corresponding equivalence relations.
-/

section EquivalenceFromPreorder

variable {Î±} (p : Î± â†’ Î± â†’ Prop) [inst : IsPreorder Î± p]

@[simp] def eqv_of_preorder (a b : Î±) : Prop := p a b âˆ§ p b a

@[refl] lemma eqv_of_preorder_refl (a : Î±) : eqv_of_preorder p a a := by simp; apply inst.refl

set_option linter.unusedSectionVars false in
@[symm] lemma eqv_of_preorder_symm {a b : Î±} : eqv_of_preorder p a b â†’ eqv_of_preorder p b a := by aesop

@[trans] lemma eqv_of_preorder_trans {a b c : Î±} :
    eqv_of_preorder p a b â†’ eqv_of_preorder p b c â†’ eqv_of_preorder p a c:= by
  simp; intros Hâ‚ Hâ‚‚ Hâ‚ƒ Hâ‚„
  constructor
  Â· exact (inst.trans a b c Hâ‚ Hâ‚ƒ)
  Â· exact (inst.trans c b a Hâ‚„ Hâ‚‚)

-- Equivalence is the unbundled form of Setoid
instance eqv_of_preorder_inst : Equivalence (eqv_of_preorder p) where
  refl := eqv_of_preorder_refl p
  symm := eqv_of_preorder_symm p
  trans := eqv_of_preorder_trans p

end EquivalenceFromPreorder

/-! ### Core Green's relations definitions

This section defines the fundamental Green's preorders and equivalences. We start with the
basic preorder definitions using `WithOne` elements, establish their preorder properties,
and then derive the equivalence relations as symmetric closures.
-/

section CoreDefinitions

variable {S} [Semigroup S]

/-- the ğ“¡ preorder: a â‰¤ğ“¡ b iff a*SÂ¹ âŠ† b*SÂ¹ -/
def R_preorder (a b : S) : Prop := âˆƒ x : SÂ¹ , â†‘a = â†‘b * x
/-- the ğ“› preorder: a â‰¤ğ“› b iff SÂ¹*a âŠ† SÂ¹*b -/
def L_preorder (a b : S) : Prop := âˆƒ x : SÂ¹, â†‘a = x * â†‘b
/-- the ğ“™ preorder: a â‰¤ğ“™ b iff SÂ¹*a*SÂ¹ âŠ† SÂ¹*b*SÂ¹ -/
def J_preorder (a b : S) : Prop := âˆƒ x y : SÂ¹, a = x * b * y

/-! Preorder notation, typed \leq\MCR -/
notation:50 f " â‰¤ğ“¡ " g:50 => R_preorder f g
notation:50 f " â‰¤ğ“› " g:50 => L_preorder f g
notation:50 f " â‰¤ğ“™ " g:50 => J_preorder f g

/-- the ğ“— preorder -/
def H_preorder (a b : S) : Prop := a â‰¤ğ“¡ b âˆ§ a â‰¤ğ“› b

notation:50 f " â‰¤ğ“— " g:50 => H_preorder f g

/-! Definitional lemmas -/
theorem R_preorder_iff (a b : S) : a â‰¤ğ“¡ b â†” âˆƒ x : SÂ¹ , â†‘a = â†‘b * x := by simp [R_preorder]
theorem L_preorder_iff (a b : S) : a â‰¤ğ“› b â†” âˆƒ x : SÂ¹, â†‘a = x * â†‘b := by simp [L_preorder]
theorem J_preorder_iff (a b : S) : a â‰¤ğ“™ b â†” âˆƒ x y : SÂ¹, a = x * b * y := by simp [J_preorder]
theorem H_preorder_iff (a b : S) : a â‰¤ğ“— b â†” a â‰¤ğ“¡ b âˆ§ a â‰¤ğ“› b := by simp [H_preorder]

/-! Reflexivity lemmas -/
@[simp] lemma R_preorder_refl (x : S) : x â‰¤ğ“¡ x := by use 1; simp
@[simp] lemma L_preorder_refl (x : S) : x â‰¤ğ“› x := by use 1; simp
@[simp] lemma J_preorder_refl (x : S) : x â‰¤ğ“™ x := by use 1, 1; simp
@[simp] lemma H_preorder_refl (x : S) : x â‰¤ğ“— x := by
  unfold H_preorder; apply And.intro <;> use 1; simp; rw [one_mul]

/-! Transitivity lemmas -/
lemma R_preorder_trans (a b c : S) : a â‰¤ğ“¡ b â†’ b â‰¤ğ“¡ c â†’ a â‰¤ğ“¡ c := by
  simp [R_preorder]; intros hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
  use (hâ‚ƒ * hâ‚)
  rw [â† mul_assoc, â† hâ‚„, hâ‚‚]
lemma L_preorder_trans (a b c : S) : a â‰¤ğ“› b â†’ b â‰¤ğ“› c â†’ a â‰¤ğ“› c := by
  unfold L_preorder; rintro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©
  use x * y; rw [hx, hy, mul_assoc]
lemma J_preorder_trans (a b c : S) : a â‰¤ğ“™ b â†’ b â‰¤ğ“™ c â†’ a â‰¤ğ“™ c := by
  unfold J_preorder; rintro âŸ¨xâ‚, yâ‚, hxâŸ© âŸ¨xâ‚‚, yâ‚‚, hyâŸ©
  use xâ‚ * xâ‚‚, yâ‚‚ * yâ‚; rw [hx, hy]; simp [mul_assoc]
lemma H_preorder_trans (a b c : S) : a â‰¤ğ“— b â†’ b â‰¤ğ“— c â†’ a â‰¤ğ“— c := by
  unfold H_preorder; rintro âŸ¨hâ‚, hâ‚‚âŸ© âŸ¨hâ‚ƒ, hâ‚„âŸ©
  constructor
  Â· apply R_preorder_trans a b c <;> assumption
  Â· apply L_preorder_trans a b c <;> assumption

/-! Preorder instances -/
instance R_is_preorder : IsPreorder S R_preorder where
  refl := R_preorder_refl
  trans := R_preorder_trans
instance L_is_preorder : IsPreorder S L_preorder where
  refl := L_preorder_refl
  trans := L_preorder_trans
instance J_is_preorder : IsPreorder S J_preorder where
  refl := J_preorder_refl
  trans := J_preorder_trans
instance H_is_preorder : IsPreorder S H_preorder where
  refl := H_preorder_refl
  trans := H_preorder_trans

/-- The equivalence relation `ğ“¡` -/
def R_eqv : S â†’ S â†’ Prop := eqv_of_preorder R_preorder
/-- The equivalence relation `ğ“›` -/
def L_eqv : S â†’ S â†’ Prop := eqv_of_preorder L_preorder
/-- The equivalence relation `ğ“™` -/
def J_eqv : S â†’ S â†’ Prop := eqv_of_preorder J_preorder
/-- The equivalence relation `ğ“—` -/
def H_eqv : S â†’ S â†’ Prop := eqv_of_preorder H_preorder

/-! Equivalence notation: typed \MCR -/
notation:50 a " ğ“¡ " b:50 => R_eqv a b
notation:50 a " ğ“› " b:50 => L_eqv a b
notation:50 a " ğ“™ " b:50 => J_eqv a b
notation:50 a " ğ“— " b:50 => H_eqv a b

/-! Definitional lemmas for equivalence relations -/
lemma R_eqv_iff (a b : S) : a ğ“¡ b â†” a â‰¤ğ“¡ b âˆ§ b â‰¤ğ“¡ a := by unfold R_eqv; simp
lemma L_eqv_iff (a b : S) : a ğ“› b â†” a â‰¤ğ“› b âˆ§ b â‰¤ğ“› a := by unfold L_eqv; simp
lemma J_eqv_iff (a b : S) : a ğ“™ b â†” a â‰¤ğ“™ b âˆ§ b â‰¤ğ“™ a := by unfold J_eqv; simp
lemma H_eqv_iff (a b : S) : a ğ“— b â†” a â‰¤ğ“— b âˆ§ b â‰¤ğ“— a := by unfold H_eqv; simp

variable {a b : S}

/-! Reflexivity Lemmas -/
@[simp] lemma R_eqv_refl : a ğ“¡ a := by constructor <;> simp
@[simp] lemma L_eqv_refl : a ğ“› a := by constructor <;> simp
@[simp] lemma J_eqv_refl : a ğ“™ a := by constructor <;> simp
@[simp] lemma H_eqv_refl : a ğ“— a := by constructor <;> simp

/-! Symmetry Lemmas -/
lemma R_eqv_symm : a ğ“¡ b â†” b ğ“¡ a := by
  constructor <;> (intros h; apply eqv_of_preorder_symm; simp_all [R_eqv_iff])
lemma L_eqv_symm : a ğ“› b â†” b ğ“› a := by
  constructor <;> (intros h; apply eqv_of_preorder_symm; simp_all [L_eqv_iff])
lemma J_eqv_symm : a ğ“™ b â†” b ğ“™ a := by
  constructor <;> (intros h; apply eqv_of_preorder_symm; simp_all [J_eqv_iff])
lemma H_eqv_symm : a ğ“— b â†” b ğ“— a := by
  constructor <;> (intros h; apply eqv_of_preorder_symm; simp_all [H_eqv_iff])
end CoreDefinitions

/-! ### Alternative characterizations of ğ“— -/

section AlternativeH

variable {S} [Semigroup S] (a b : S)

/-- The ğ“— equivalence relation is the intersection of ğ“¡ and ğ“› equivalences. -/
theorem H_eqv_iff_L_and_R : a ğ“— b â†” a ğ“¡ b âˆ§ a ğ“› b := by
  simp [H_eqv, H_preorder_iff]
  rw [@and_comm _ (b â‰¤ğ“› a), and_assoc, â† @and_assoc _ _ (b â‰¤ğ“¡ a)]
  rw [ â† L_eqv_iff, and_comm, and_assoc, @and_comm (b â‰¤ğ“¡ a) _, â†  R_eqv_iff, and_comm]

end AlternativeH

/-! ### WithOne-free characterizations of Preorders

This section provides alternative characterizations of Green's preorders that avoid
explicit reference to `WithOne` elements, which can be more convenient for certain proofs.
-/

section WithoutOneCharacterizations

variable {S} [Semigroup S] (a b : S)

theorem R_preorder_iff_without_one : a â‰¤ğ“¡ b â†” a = b âˆ¨ âˆƒ x, a = b * x := by
  unfold R_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => left; simp_all
    | coe y => right; use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro h; cases h with
    | inl h => use (1 : SÂ¹); simp_all
    | inr h => obtain âŸ¨x, hxâŸ© := h; use x; simp [â† WithOne.coe_mul, hx]

theorem L_preorder_iff_without_one : a â‰¤ğ“› b â†” a = b âˆ¨ âˆƒ x, a = x * b := by
  unfold L_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => left; simp_all
    | coe y => right; use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro h; cases h with
    | inl h => use (1 : SÂ¹); simp_all
    | inr h => obtain âŸ¨x, hxâŸ© := h; use x; simp [â† WithOne.coe_mul, hx]

theorem J_preorder_iff_without_one : a â‰¤ğ“™ b â†” a = b âˆ¨ a â‰¤ğ“› b âˆ¨ a â‰¤ğ“¡ b âˆ¨ âˆƒ x y, a = x * b * y := by
  constructor
  Â· intro hJ; obtain âŸ¨x, y, hxyâŸ© := hJ; cases' x with x' <;> cases' y with y'
    Â· left; simp_all
    Â· right; right; left; use â†‘y'; exact hxy
    Â· right; left; use â†‘x'; exact hxy
    Â· right; right; right; use x', y'; simp [â† WithOne.coe_inj, hxy]
  Â· rintro (h | h | h | âŸ¨x, y, hxyâŸ©)
    Â· rw [h]; exact J_preorder_refl b
    Â· obtain âŸ¨x', hxâŸ© := h; use x', 1; simp [hx]
    Â· obtain âŸ¨y', hyâŸ© := h; use 1, y'; simp [hy]
    Â· use â†‘x, â†‘y; simp [hxy, WithOne.coe_mul]

end WithoutOneCharacterizations

/-! ### Monoid-specific characterizations of Preorders

This section provides specialized versions of Green's preorder definitions for monoids,
demonstrating how the `WithOne` construction behaves like the original monoid.
-/

section MonoidCharacterizations

variable {M} [Monoid M] (a b : M)

theorem R_preorder_iff_monoid : a â‰¤ğ“¡ b â†” âˆƒ x, a = b * x := by
  unfold R_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => use 1; simp [â† WithOne.coe_inj, hx]
    | coe y => use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro âŸ¨x, hxâŸ©; use (â†‘x : MÂ¹); simp [â† WithOne.coe_inj, hx]

theorem L_preorder_iff_monoid : a â‰¤ğ“› b â†” âˆƒ x, a = x * b := by
  unfold L_preorder; constructor
  Â· rintro âŸ¨x, hxâŸ©; cases x with
    | one => use 1; simp [â† WithOne.coe_inj, hx]
    | coe y => use y; simp [â† WithOne.coe_inj, hx]
  Â· rintro âŸ¨x, hxâŸ©; use (â†‘x : MÂ¹)
    rw [â† WithOne.coe_inj, WithOne.coe_mul] at hx; exact hx

theorem J_preorder_iff_monoid : a â‰¤ğ“™ b â†” âˆƒ x y, a = x * b * y := by
  unfold J_preorder; constructor
  Â· rintro âŸ¨x, y, hxyâŸ©; cases' x with x' <;> cases' y with y'
    Â· use 1, 1; simp_all
    Â· use 1, y'; simp [â† WithOne.coe_inj, hxy]
    Â· use x', 1; simp [â† WithOne.coe_inj, hxy]
    Â· use x', y'; simp [â† WithOne.coe_inj, hxy]
  Â· rintro âŸ¨x, y, hxyâŸ©; use (â†‘x : MÂ¹), (â†‘y : MÂ¹); simp [â† WithOne.coe_inj, hxy]

end MonoidCharacterizations

/-! ### Ideal-based characterizations

This section provides an alternative approach to Green's relations through principal ideals.
-/

section IdealCharacterizations

/-- the left ideal `M â€¢ a` -/
def left_ideal (M) [Monoid M] (a : M) : Set (M) := {x | âˆƒ y, x = y * a}

/-- the right ideal `a â€¢ M` -/
def right_ideal (M) [Monoid M] (a : M) : Set (M) := {x | âˆƒ m, x = a * m}

/-- the two_sided ideal ` M â€¢â€¢ a` -/
def two_sided_ideal (M) [Monoid M] (a : M) : Set (M) := {x | âˆƒ y z, x = y * a * z}

/-! Ideals of sets (rather than ideals of elements) -/

def left_ideal_set (M) [Monoid M] (A : Set M) : Set (M) := {x | âˆƒ (a : A) (y : M), x = y * a}

def right_ideal_set (M) [Monoid M] (A : Set M) : Set (M) := {x | âˆƒ (a : A) (y : M), x = a * y}

def two_sided_ideal_set (M) [Monoid M] (A : Set M) : Set (M) := {x | âˆƒ (a : A) (y z : M), x = y * a * z}

/-! Ideal notation, typed \bub -/
notation:65 M " â€¢ " a:66 => left_ideal M a
notation:65 a:66 " â€¢ " M => right_ideal M a
notation:65 M " â€¢â€¢ " a:66  => two_sided_ideal M a

/-! Ideal set notation  -/
notation:65 M " â€¢Ë¢ " A:66 => left_ideal_set M A
notation:65 A:66 " â€¢Ë¢ " M => right_ideal_set M A
notation:65 M " â€¢â€¢Ë¢ " A:66  => two_sided_ideal_set M A

variable {S} [Semigroup S] (a b : S)

/-- The left ideal of `a * b` is contained in the left ideal of `b`. -/
lemma left_ideal_subset : (SÂ¹ â€¢ (a * b)) âŠ† (SÂ¹ â€¢ b) := by
  simp [left_ideal]; intro x; use (x * a); simp [mul_assoc]

/-- The right ideal of `a * b` is contained in the right ideal of `a`. -/
lemma right_ideal_subset : ((a * b) â€¢ SÂ¹) âŠ† (a â€¢ SÂ¹) := by
  simp [right_ideal]; intro x; use (b * x); simp [mul_assoc]

/-! Preorder characterizations from ideals -/

theorem L_preorder_iff_ideal : a â‰¤ğ“› b â†” (SÂ¹ â€¢ a) âŠ† (SÂ¹ â€¢ b) := by
  rw [L_preorder_iff_without_one]
  constructor
  Â· intro h; cases h with
    | inl heq => simp [heq]
    | inr h => obtain âŸ¨x, hxâŸ© := h; rw [hx]; simp [left_ideal_subset]
  Â· intro h; simp_all [left_ideal]
    specialize h â†‘a 1; simp_all [â† L_preorder_iff_without_one, L_preorder]

theorem R_preorder_iff_ideal : a â‰¤ğ“¡ b â†” (a â€¢ SÂ¹) âŠ† (b â€¢ SÂ¹) := by
  rw [R_preorder_iff_without_one]
  constructor
  Â· intro h; cases h with
    | inl heq => simp [heq]
    | inr h => obtain âŸ¨x, hxâŸ© := h; rw [hx]; simp [right_ideal_subset]
  Â· intro h; simp_all [right_ideal]
    specialize h â†‘a 1; simp_all [â† R_preorder_iff_without_one, R_preorder]

theorem J_preorder_iff_ideal : a â‰¤ğ“™ b â†” (SÂ¹ â€¢â€¢ a) âŠ† (SÂ¹ â€¢â€¢ b) := by
  constructor
  Â· simp [J_preorder, two_sided_ideal]
    rintro x y Hreach z t u Hz; subst Hz
    use (t * x), (y * u); simp_all [mul_assoc]
  Â· simp [J_preorder, two_sided_ideal]
    intros H
    specialize H â†‘a 1 1; simp_all

/-! Equivalence relation characterizations from ideals -/

theorem L_eqv_iff_ideal : a ğ“› b â†” (SÂ¹ â€¢ a) = (SÂ¹ â€¢ b) := by
  simp [L_eqv_iff, L_preorder_iff_ideal, antisymm_iff]

theorem R_eqv_iff_ideal : a ğ“¡ b â†” (a â€¢ SÂ¹) = (b â€¢ SÂ¹) := by
  simp [R_eqv_iff, R_preorder_iff_ideal, antisymm_iff]

theorem J_eqv_iff_ideal : a ğ“™ b â†” (SÂ¹ â€¢â€¢ a) = (SÂ¹ â€¢â€¢ b) := by
  simp [J_eqv_iff, J_preorder_iff_ideal, antisymm_iff]

/- Relating one and two sided ideals (should we keep these?) -/

lemma two_sided_ideal_iff : ((SÂ¹ â€¢ a) â€¢Ë¢ SÂ¹) = (SÂ¹ â€¢â€¢ a) := by
  simp [two_sided_ideal]
  apply Set.setOf_inj.mpr; ext x
  constructor
  Â· simp; intro b hb c hc; subst hc
    simp [left_ideal] at hb; obtain âŸ¨d, hdâŸ© := hb; subst hd
    use d, c
  Â· rintro âŸ¨y, z, hâŸ©; subst h; simp
    use y * â†‘a; simp [left_ideal]; use y

lemma two_sided_ideal_iff' : (SÂ¹ â€¢Ë¢ (a â€¢ SÂ¹)) = (SÂ¹ â€¢â€¢ a) := by
  simp [two_sided_ideal]
  apply Set.setOf_inj.mpr; ext x
  constructor
  Â· simp; intro b hb c hc; subst hc
    simp [right_ideal] at hb; obtain âŸ¨d, hdâŸ© := hb; subst hd
    use c, d; rw [â† mul_assoc]
  Â· rintro âŸ¨y, z, hâŸ©; subst h; simp
    use â†‘a * z; simp [right_ideal, â† mul_assoc]; use y

end IdealCharacterizations

/-! ### Green's ğ““-relation and relation composition

This section introduces relation composition and uses it to define Green's ğ““-relation.
We establish the commutativity of Green's ğ“¡ and ğ“› relations under composition, which
allows an alternate characterization of the ğ““-relation.
-/

section DRelation

variable {S} [Semigroup S] {a b: S}

/-! Compatibility of Green's relations with multiplication -/

theorem R_preorder_lmult_compat (h : a â‰¤ğ“¡ b) (c) : c * a â‰¤ğ“¡ c * b := by
  obtain âŸ¨x, hxâŸ© := h; use x
  simp [mul_assoc, hx]

theorem R_eqv_lmult_compat (h : a ğ“¡ b) (c) : c * a ğ“¡ c * b := by
  simp_all [R_eqv_iff]
  cases h; constructor <;> (apply R_preorder_lmult_compat; assumption)

theorem L_preorder_rmult_compat (h : a â‰¤ğ“› b) (c) : a * c â‰¤ğ“› b * c := by
  obtain âŸ¨x, hxâŸ© := h; use x
  simp [â† mul_assoc, hx]

theorem L_eqv_rmult_compat (h : a ğ“› b) (c) : a * c ğ“› b * c := by
  simp_all [L_eqv_iff]
  cases h; constructor <;> (apply L_preorder_rmult_compat; assumption)

/-! Relation composition and Green's D-relation -/

/-- Composition of binary relations -/
def rel_comp {Î± : Type*} (r s : Î± â†’ Î± â†’ Prop) : Î± â†’ Î± â†’ Prop :=
  Î» a c => âˆƒ b, r a b âˆ§ s b c

notation:50 f " âˆ˜á´¿ " g:50 => rel_comp f g

/-- Green's D-relation, defined as the composition of R and L relations -/
def D_eqv : S â†’ S â†’ Prop := R_eqv âˆ˜á´¿ L_eqv
infix:50 " ğ““ " => D_eqv

/-- Straight Definitional lemma of Green's `ğ““` equivalence -/
lemma D_eqv_iff : a ğ““ b â†” âˆƒ x, a ğ“¡ x âˆ§ x ğ“› b := by simp [D_eqv, rel_comp]

/-- **Green's Commutativity**: R and L relations commute under composition -/
theorem R_L_comm: (L_eqv âˆ˜á´¿ R_eqv) a b â†” (R_eqv âˆ˜á´¿ L_eqv) a b := by
  unfold rel_comp
  constructor
  Â· rintro âŸ¨c, hl, hrâŸ©
    rcases _ : hl with âŸ¨hl1, hl2âŸ©; rcases _ : hr with âŸ¨hr1, hr2âŸ©
    rw [L_preorder_iff_without_one] at hl1;
    cases hl1 with
    | inl heq => subst heq; use b; constructor; assumption; apply eqv_of_preorder_refl
    | inr hneq =>
      obtain âŸ¨u, huâŸ© := hneq; subst hu; use u* b
      rw [R_preorder_iff_without_one] at hr2
      cases hr2 with
      | inl heq => subst heq; constructor; apply eqv_of_preorder_refl; assumption
      | inr hneq =>
        obtain âŸ¨v, hvâŸ© := hneq; subst hv
        constructor
        Â· apply R_eqv_lmult_compat; assumption
        Â· rw [â† mul_assoc]; apply L_eqv_rmult_compat; assumption
  Â· rintro âŸ¨c, hl, hrâŸ©
    rcases _ : hl with âŸ¨hl1, hl2âŸ©; rcases _ : hr with âŸ¨hr1, hr2âŸ©
    rw [R_preorder_iff_without_one] at hl1;
    cases hl1 with
    | inl heq => subst heq; use b; constructor; assumption; apply eqv_of_preorder_refl
    | inr hneq =>
      obtain âŸ¨v, hvâŸ© := hneq; subst hv; use b*v
      rw [L_preorder_iff_without_one] at hr2
      cases hr2 with
      | inl heq => subst heq; constructor; apply eqv_of_preorder_refl; assumption
      | inr hneq =>
        obtain âŸ¨u, huâŸ© := hneq; subst hu
        constructor
        Â· apply L_eqv_rmult_compat; assumption
        Â· rw [ mul_assoc]; apply R_eqv_lmult_compat; assumption

/-- Alternate Def of Green's `ğ““` equivalence -/
theorem D_eqv_iff_comm : a ğ““ b â†” âˆƒ x, a ğ“› x âˆ§ x ğ“¡ b := by
  unfold D_eqv; rw [â† R_L_comm]; simp [rel_comp]

@[simp] lemma D_eqv_refl (a : S) : a ğ““ a := by
  simp [D_eqv_iff]; use a; constructor <;> apply eqv_of_preorder_refl

lemma D_eqv_symm : a ğ““ b â†’ b ğ““ a := by
  rw [D_eqv_iff, D_eqv_iff_comm]
  Â· rintro âŸ¨x, âŸ¨hx1, hx2âŸ©âŸ©; use x; constructor
    Â· exact hx2.symm
    Â· exact hx1.symm

/-- The ğ““-relation is preserved by ğ“›-equivalent elements on the right.
If `a ğ““ b` and `b ğ“› c`, then `a ğ““ c`. -/
lemma D_eqv_closed_under_L {a b c : S} : a ğ““ b â†’ b ğ“› c â†’ a ğ““ c := by
  simp [D_eqv_iff]; intros x h1 h2 h3; use x; constructor
  Â· exact h1
  Â· exact (eqv_of_preorder_trans L_preorder h2 h3)

/-- The ğ““-relation is preserved by ğ“¡-equivalent elements on the right.
If `a ğ““ b` and `b ğ“¡ c`, then `a ğ““ c`. -/
lemma D_eqv_closed_under_R {a b c : S} : a ğ““ b â†’ b ğ“¡ c â†’ a ğ““ c := by
  simp [D_eqv_iff_comm]; intros x h1 h2 h3; use x; constructor
  Â· exact h1
  Â· exact (eqv_of_preorder_trans R_preorder h2 h3)

/-- The ğ““-relation is transitive. This is proved using closure under ğ“¡ and ğ“›. -/
lemma D_eqv_trans {a b c : S} : a ğ““ b â†’ b ğ““ c â†’ a ğ““ c := by
  intros h1 h2
  obtain âŸ¨y, âŸ¨hy1, hy2âŸ©âŸ© := h2
  have hd1 : a ğ““ y := by apply D_eqv_closed_under_R; exact h1; assumption
  apply D_eqv_closed_under_L hd1 hy2

/-- The ğ““-relation is an equivalence relation on `S`. This instance combines the
reflexivity, symmetry and transitivity proofs. -/
instance D_eqv_inst : Equivalence (fun a b : S => a ğ““ b) where
  refl := D_eqv_refl
  symm := D_eqv_symm
  trans := D_eqv_trans

end DRelation


/-! ### Translations and Set-defined R/L classes

This section defines translation on semigroups and provides a set (not necessarily finite)
description of R/L classes. These definitions provide foundations for proving Green's Lemma.
-/

section Translations

variable {S} [Semigroup S] {a b: S}

def R_translation (a : S) : S â†’ S := (Â· * a)
def R_translation_op (a : Sáµáµ’áµ–) : Sáµáµ’áµ– â†’ Sáµáµ’áµ– := (Â· * a)
notation:50 "Ïáµ£" a => R_translation a
infixr:70 " â‹†Ïáµ£ " => R_translation

def L_translation (a : S) : S â†’ S := (a * Â·)
def L_translation_op (a : Sáµáµ’áµ–) : Sáµáµ’áµ– â†’ Sáµáµ’áµ– := (a * Â·)
notation:50 "Ïâ‚—" a => L_translation a
infixr:70 " â‹†Ïâ‚— " => L_translation

def R_class_set (x : S) : Set (S) :=
  {a | a ğ“¡ x}
def L_class_set (x : S) : Set (S) :=
  { a | a ğ“› x}
def J_class_set (x : S) : Set (S) :=
  { a | a ğ“™ x}
def H_class_set (x : S) : Set (S) :=
  { a | a ğ“— x}

end Translations
