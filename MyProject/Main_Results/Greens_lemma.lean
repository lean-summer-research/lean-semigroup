import MyProject.Main_Results.DJ_Theorem

/-! # Greens Lemma

This file proves Green's lemma, which is the following:
Let `a ğ“¡ b` such that `a = b * u` and `b = a * v`,
then the map `x â†¦ x * v` is a bijection from the ğ“›-classes of `a` to the ğ“›-class of `b`
and the map `x â†¦ x * u` is its inverse bijection. Additionally, these bijections preserve ğ“— classes.

We also prove the dual, left translation version of this lemma

## Main Definitions

Let `a ğ“¡ b` such that `a = b * u` and `b = a * v`

* `right_translations_inverse` - the right translation by `u` is the inverse of the right
translation by `v` for all elements `x` in the ğ“› class of `a`.

* `right_translation_bijection` - the right translation by `u` from the ğ“›-class of `a` to
the ğ“›-class of `b` is a bijection.

* `right_translation_preserves_H` - This bijection preserves ğ“— classes

We also prove the dual, left translation version of these.
-/


section Greens_Lemma

open MulOpposite

variable {S} [Semigroup S] {a b u v x y : S}

/-- If `a ğ“¡ b` such that `a = b * u` and `b = a * v`, then right translation by `v * u`
on any element `x` L-equivalent to `a` is the identity. -/
lemma right_translation_id (ha : a = b * u) (hb : b = a * v) (hx : x ğ“› a) : x * v * u = x := by
  rw [L_eqv_iff, L_preorder_iff_without_one] at hx
  rcases hx with âŸ¨(heq | âŸ¨x, hxâŸ©), _âŸ©
  Â· subst heq; rw [â† hb, â† ha]
  Â· subst hx; rw [mul_assoc x, â† hb, mul_assoc, â† ha]

-- Dual version
lemma left_translation_id (ha : a = u * b) (hb : b = v * a) (hx : x ğ“¡ a) : u * v * x = x := by
  rw [eq_mul_iff_op, R_eqv_iff_op, mul_mul_eq_iff_op] at *
  apply right_translation_id ha hb hx

-- TODO: is the below lemma ever used?

/-- If `a ğ“¡ b` such that `a = b * u` and `b = a * v`,
then `Ïáµ£ u` inverts `Ïáµ£ v` on the ğ“› class of `a` -/
theorem right_translations_inverse (ha : a = b * u) (hb : b = a * v) :
    Set.InvOn (Ïáµ£ u) (Ïáµ£ v) (L_class_set a) (L_class_set b) := by
  simp [Set.InvOn, Set.LeftInvOn, L_class_set, R_translation]
  constructor
  /- Becuase `x ğ“› a`, we have a `y` such that `x = y * a`,
   thus `x * v * u = y * a * v * u = y * a = x` -/
  Â· intros x hL
    apply right_translation_id ha hb hL
  Â· intros x hL
    apply right_translation_id hb ha hL

-- Dual Version
theorem left_translations_inverse (ha : a = u * b) (hb : b = v * a) :
    Set.InvOn (Ïâ‚— u) (Ïâ‚— v) (R_class_set a) (R_class_set b) := by
  simp [Set.InvOn, Set.LeftInvOn, R_class_set, L_translation]
  constructor
  Â· intros x hR; rw [â† mul_assoc]
    apply left_translation_id ha hb hR
  Â· intros x hR; rw [â† mul_assoc]
    apply left_translation_id hb ha hR

/-- If `a ğ“¡ b` such that `a = b * u`, then `Ïáµ£ u` is a bijection
from the ğ“› class of `b` to the ğ“› class of `a` -/
theorem right_translation_bijection (ha: a = b * u) (hb : b = a * v):
    Set.BijOn (Ïáµ£ v) (L_class_set a) (L_class_set b) := by
  refine Set.BijOn.mk ?_ ?_ ?_
  Â· intros x hx
    simp [L_class_set, R_translation] at *
    subst b
    apply L_eqv_rmult_compat; exact hx
  Â· intro x hx y hy hxy
    simp [L_class_set, R_translation] at *
    have hinvx := right_translation_id ha hb hx
    have hinvy := right_translation_id ha hb hy
    rw [â† hinvx, â† hinvy, hxy]
  Â· intros y hy
    simp [R_translation, L_class_set] at *
    use y * u
    constructor
    Â· rw [ha]
      apply L_eqv_rmult_compat; exact hy
    Â· apply right_translation_id hb ha hy

-- Dual version
theorem left_translation_bijection (ha : a = u * b) (hb : b = v * a) :
    Set.BijOn (Ïâ‚— v) (R_class_set a) (R_class_set b) := by
  refine Set.BijOn.mk ?_ ?_ ?_
  Â· intros x hx
    simp [R_class_set, L_translation] at *
    subst b
    apply R_eqv_lmult_compat; exact hx
  Â· intro x hx y hy hxy
    simp [R_class_set, L_translation] at *
    have hinvx := left_translation_id ha hb hx
    have hinvy := left_translation_id ha hb hy
    rw [â† hinvx, â† hinvy, mul_assoc, hxy, â† mul_assoc]
  Â· intros y hy
    simp [L_translation, R_class_set] at *
    use u * y
    constructor
    Â· rw [ha]
      apply R_eqv_lmult_compat; exact hy
    Â· rw [â† mul_assoc]; apply left_translation_id hb ha hy

/-- If `a ğ“¡ b` such that `b = a * v`, then the bijection `Ïáµ£ v` preserves ğ“— classes -/
theorem right_translation_preserves_H (ha: a = b * u) (hb: b = a * v)
  (hx : x âˆˆ L_class_set a) (hy : y âˆˆ L_class_set a) :
    (x ğ“— y) â†” (x * v) ğ“— (y * v) := by
  simp [L_class_set, R_translation, H_eqv_iff_L_and_R] at *
  have hidx : x * v * u = x := right_translation_id ha hb hx
  have hidy : y * v * u = y := right_translation_id ha hb hy
  have hxux : x * v ğ“¡ x := by
    simp [R_eqv, R_preorder_iff_without_one]
    right; use u; exact hidx.symm
  have hyuy : y ğ“¡ y * v := by
    simp [R_eqv, R_preorder_iff_without_one]
    right; use u; exact hidy.symm
  clear ha hb
  constructor
  Â· rintro âŸ¨hr, hlâŸ©
    constructor
    Â· apply R_eqv_trans hxux
      apply R_eqv_trans hr
      exact hyuy
    Â· apply L_eqv_rmult_compat; exact hl
  Â· rintro âŸ¨hr, hlâŸ©
    constructor
    Â· apply R_eqv_trans hxux.symm
      apply R_eqv_trans hr
      exact hyuy.symm
    Â· apply L_eqv_trans hx hy.symm

-- Dual version
theorem left_translation_preserves_H (ha : a = u * b) (hb : b = v * a)
  (hx : x âˆˆ R_class_set a) (hy : y âˆˆ R_class_set a) :
    (x ğ“— y) â†” (v * x) ğ“— (v * y) := by
  rw [eq_mul_iff_op, R_class_iff_op, H_eqv_iff_op, H_eqv_iff_op (v * x), op_mul, op_mul] at *
  apply right_translation_preserves_H ha hb hx hy


end Greens_Lemma
