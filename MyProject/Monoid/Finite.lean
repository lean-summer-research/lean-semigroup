import MyProject.Monoid.Defs

/-!
# Finiteness in Semigroups
This file begins by defining an exponentiation operation for semigroups using positive natural numbers.
We then prove statments about idempotent elements in finite semigroups and monoids.
These statments are used in the latter parts of the file to prove the DJ Theorem.

## Main Definitions
- `pnatPow`: Exponentiation for semigroups over positive naturals.

## Main Statements
- `Semigroup.exists_idempotent_pow` - and a version for monoids
- `Semigroup.exists_pow_mul_of_sandwich` - and a version for monoids
- `D_eqv_iff_J_finite`: The DJ Theorem, which states that in finite Monoids,
  the ğ““-relation equals the ğ“™-relation.
-/

/-
### Positive Natural Number Exponentiation for Semigroups
Exponentiation in monoids is defined for natural number exponents, but here we require non-zero
natural numbers (`â„•+`) because `x^0` is not well defined in a semigroup context.

We also prove lemmas about the properties of this exponentiation operation and of the `â„•+` type.

The simp-tagged lemmas collectively normalize power expressions when calling `simp`.
For example, `(a * b) ^ n * (a * b) ^ m * a ^ 1` normalizes to `a * (b * a) ^ (n + m)`.

Analogous definitions and lemmas for exponentiation in monoids can be found in
`Mathlib.Algebra.Group.Defs`.
-/

namespace PNat

/-- If `m < n` for positive naturals, then there exists a positive `k` such that `n = k + m`. -/
lemma exists_eq_add_of_lt {m n : â„•+} (m_lt_n : m < n) : âˆƒ (k : â„•+), n = k + m := by
  use n - m
  pnat_to_nat; omega

/-- For positive naturals, if `m < n` then `m + (n - m) = n`. -/
@[simp] lemma add_sub_cancel' {n m : â„•+} (m_lt_n : m < n) : m + (n - m) = n := by
  induction m using PNat.recOn <;> (pnat_to_nat; omega)

/-- For positive naturals, `n < 2 * n * m` -/
lemma lt_2_mul_mul (n m : â„•+) : n < 2 * n * m := by
  induction m using PNat.recOn with
  | one => pnat_to_nat; omega
  | succ m ih => pnat_to_nat; ring_nf; omega

variable {S : Type*} [Semigroup S]

/-- Exponentiation for semigroups over positive naturals -/
def pnatPow (x : S) (n : â„•+) : S := @PNat.recOn n (fun _ => S) x (fun _ ih => ih * x)

/-- Provides the notation `a ^ n` for `pnatPow a n` -/
instance hPow : Pow S â„•+ := âŸ¨pnatPowâŸ©

variable (x y : S) (n m : â„•+)

/-- For any element `x` of a semigroup, `x` raised to the power `1` equals `x`. -/
@[simp] lemma pow_one : x ^ (1 : â„•+) = x := by rfl

/-- Exponentiation satisfies the successor property -/
@[simp] lemma pow_succ : (x ^ n) * x = x ^ (n + 1) := by induction n using PNat.recOn <;> rfl

/-- Power of a sum equals the product of powers -/
@[simp] lemma pow_add : x ^ m * x ^ n = x ^ (m + n) := by
  induction n using PNat.recOn with
  | one => rw [pow_one, pow_succ]
  | succ k ih => simp_rw [â† add_assoc, â† pow_succ, â† mul_assoc, ih]

/-- Multiplicative associativity for powers -/
@[simp] lemma mul_pow_mul : (x * y) ^ n * x = x * (y * x) ^ n := by
  induction n using PNat.recOn with
  | one => simp [â† mul_assoc]
  | succ n ih => simp only [â† pow_succ, â† mul_assoc, ih]

/-- Powers of the same element commute with each other -/
lemma pow_mul_comm : x ^ m * x ^ n = x ^ n * x ^ m := by rw [pow_add, add_comm, pow_add]

/-- For every `x : S` and `n : â„•+`, the power `x ^ n` commutes with `x` -/
@[simp] lemma pow_mul_comm' : x ^ n * x = x * x ^ n := by
  induction n using PNat.recOn with
  | one    => rfl
  | succ k ih => rw [â† pow_succ, â† mul_assoc, ih]

/-- The power of a power equals the power of the product of exponents -/
@[simp] lemma pow_mul : (x ^ n) ^ m = x ^ (m * n) := by
  induction m using PNat.recOn with
  | one    => rw [one_mul, pow_one]
  | succ k ih => simp_rw [â† pow_succ, ih, pow_add]; congr; ring

/-- Right-associated powers commute -/
lemma pow_right_comm : (x ^ m) ^ n = (x ^ n) ^ m := by simp only [pow_mul, mul_comm]

/-- Bridge between PNat powers and standard Nat powers in monoids -/
lemma pow_pnat_to_nat {M} [Monoid M] (x : M) (n : â„•+) :
    x ^ n = x ^ (n : â„•) := by
  induction n with
  | one => simp [PNat.pnatPow]
  | succ n' ih =>
    rw [PNat.add_coe]; simp
    rw [ â† PNat.pow_succ, â† Nat.succ_eq_add_one, _root_.pow_succ, ih]

end PNat

/-! # Idempotent Elements in Finite Semigroups -/

namespace Semigroup

variable {S : Type*} [Semigroup S]

/-- If `x` is idempotent, then raising `x` to any positive power yields `x`. -/
lemma pow_succ_eq {x : S} (n : â„•+) (h_idem : IsIdempotentElem x) : x ^ n = x := by
  induction n using PNat.recOn with
  | one    => rfl
  | succ n' ih => rw [â† PNat.pow_succ, ih, h_idem]

/-- If `S` is finite then for any `x : S` there exist
exponents such that the power eventually repeats. -/
lemma exists_repeating_pow [Finite S] (x : S) : âˆƒ (m n : â„•+), x ^ m * x ^ n = x ^ m := by
  obtain âŸ¨o, p, hop, heqâŸ© : âˆƒ o p : â„•+, o â‰  p âˆ§ x ^ o = x ^ p := by
    apply Finite.exists_ne_map_eq_of_infinite
  simp_all only [ne_eq, PNat.pow_add]; pnat_to_nat
  rcases (Nat.lt_or_gt_of_ne hop) with (o_lt_p | p_lt_o)
  Â· use o, p - o; simp_all
  Â· use p, o - p; simp_all

/-- If two powers of an element `x : S` are idempotent, then they are equal. -/
theorem pow_idempotent_unique {x : S} {m n : â„•+}
(hm : IsIdempotentElem (x ^ m)) (hn : IsIdempotentElem (x ^ n)) : x ^ m = x ^ n := by
  rw [â† pow_succ_eq n hm, PNat.pow_right_comm, pow_succ_eq m hn]

/-- In a finite semigroup `S`, the subsemigroup generated by any element contains an idempotent -/
theorem exists_idempotent_pow [Finite S] (x : S) : âˆƒ (m : â„•+), IsIdempotentElem (x ^ m) := by
  -- `n` is the length of the pre-period (tail),
  --`loop_size` is the length of the cycle.
  obtain âŸ¨n, loop_size, loop_eqâŸ© := exists_repeating_pow x
  -- The `loop` lemma formalizes that once powers of `a` enter the cycle,
  -- adding further multiples of `loop_size` to the exponent doesn't change the result.
  have loop : âˆ€ (loop_count start : â„•+),
      n < start â†’ x ^ (start + loop_count * loop_size) = x ^ start := by
    intro loop_count
    induction loop_count using PNat.recOn with
    | one =>
      intro start n_lt_start
      obtain âŸ¨diff, hdiffâŸ© := PNat.exists_eq_add_of_lt n_lt_start
      simp_all only [PNat.pow_add, one_mul, â† PNat.pow_add, mul_assoc]
    | succ loop_count' ih =>
      intro start n_lt_start
      obtain âŸ¨diff, hdiffâŸ© := PNat.exists_eq_add_of_lt n_lt_start
      subst hdiff
      specialize ih (diff + n)
      apply ih at n_lt_start
      have h_arith :
        (loop_count' + 1) * loop_size = (loop_count' * loop_size) + loop_size := by ring
      simp_rw [h_arith, â† add_assoc, â† PNat.pow_add] at *
      rw [n_lt_start, mul_assoc, loop_eq]
  -- We choose `2 * n * loop_size` as the exponent for our idempotent element.
  -- This ensures the exponent is beyond the pre-period `n` and is a multiple of `loop_size`.
  use 2 * n * loop_size
  unfold IsIdempotentElem
  specialize loop (2 * n) (2 * n * loop_size)
  simp_all only [PNat.pow_add]
  -- Apply the `loop` lemma. The condition `n < 2 * n * loop_size` is met by `PNat.lt_2_mul_mul`.
  apply loop
  exact PNat.lt_2_mul_mul n (loop_size)

lemma exists_pow_mul_of_sandwich [Finite S] (x y z : S) (h : x * y * z = y) :
    âˆƒ n : â„•+,  x ^ n * y = y := by
  have loop : âˆ€ m : â„•+, x ^ m * y * z ^ m = y := by
    intro m
    induction m using PNat.recOn with
    | one => simp_all
    | succ m ih =>
      simp [â† PNat.pow_succ]
      rw [â† PNat.pow_mul_comm', â† mul_assoc, mul_assoc (x ^ m), mul_assoc (x ^ m)]
      rw [h, ih]
  -- Apply `loop` to the idempotent power of `x`
  obtain âŸ¨k, hkâŸ© := exists_idempotent_pow x
  use k
  have hLoop := loop k
  rw [â† hLoop]
  unfold IsIdempotentElem at hk
  simp_rw [â† mul_assoc, hk]

open MulOpposite

-- DUal to above
lemma exists_mul_pow_of_sandwich [Finite S] (x y z : S) (h : x * y * z = y) :
    âˆƒ n : â„•+, y * z ^ n = y := by
  have loop : âˆ€ m : â„•+, x ^ m * y * z ^ m = y := by
    intro m
    induction m using PNat.recOn with
    | one => simp_all
    | succ m ih =>
      simp [â† PNat.pow_succ]
      rw [â† PNat.pow_mul_comm', â† mul_assoc, mul_assoc (x ^ m), mul_assoc (x ^ m)]
      rw [h, ih]
  obtain âŸ¨k, hkâŸ© := exists_idempotent_pow z
  use k
  have hLoop := loop k
  rw [â† hLoop]
  unfold IsIdempotentElem at hk
  rw [mul_assoc, hk]

end Semigroup

/-! We now give versions of the previous proofs for Finite Monoids -/

namespace Monoid

variable {M : Type*} [Monoid M] [Finite M]

/-- Every element in a finite monoid has a non-zero idempotent power -/
theorem exists_idempotent_pow (x : M) :
    âˆƒ (n : â„•), IsIdempotentElem (x ^ n) âˆ§ n â‰  0:= by
  obtain âŸ¨m, hmâŸ© := Semigroup.exists_idempotent_pow x
  use m; simp_all only [IsIdempotentElem]
  constructor
  Â· rwa [â† PNat.pow_pnat_to_nat]
  Â· simp [PNat.ne_zero]

lemma exists_pow_mul_of_sandwich {x y z : M} (h : x * y * z = y) :
    âˆƒ n : â„•,  x ^ n * y = y âˆ§ n â‰  0 := by
  obtain âŸ¨n, hnâŸ© := Semigroup.exists_pow_mul_of_sandwich x y z h
  use n
  constructor
  Â· simp_all [â† PNat.pow_pnat_to_nat]
  Â· simp_all [PNat.ne_zero]

-- dual to above
lemma exists_mul_pow_of_sandwich {x y z : M} (h : x * y * z = y) :
    âˆƒ n : â„•, y * z ^ n = y âˆ§ n â‰  0 := by
  obtain âŸ¨n, hnâŸ© := Semigroup.exists_mul_pow_of_sandwich x y z h
  use n
  constructor
  Â· simp [â† PNat.pow_pnat_to_nat, hn]
  Â· simp

end Monoid

/-! ### DJ Theorem -/

variable {M : Type*} [Monoid M] {x y z : M}

/-- Every ğ““-related pair is ğ“™-related. -/
@[simp] theorem JEquiv.of_dEquiv (h : x ğ““ y): x ğ“™ y := by
  simp_all [DEquiv, JEquiv]
  rcases h with âŸ¨z, âŸ¨âŸ¨t, hRâ‚âŸ©, âŸ¨u, hRâ‚‚âŸ©âŸ© , âŸ¨âŸ¨v, hLâ‚âŸ©, âŸ¨w, hLâ‚‚âŸ©âŸ©âŸ©
  constructor
  Â· use v, t
    rw [â† hRâ‚, â† hLâ‚]
  Â· use w, u
    rw [â† hLâ‚‚, â† hRâ‚‚, â† mul_assoc]

variable [Finite M]

@[simp] theorem LEquiv.of_le_and_jEquiv (hL : x â‰¤ğ“› y) (hJ: x ğ“™ y) : x ğ“› y := by
  simp_all [LEquiv]
  obtain âŸ¨_, hJâŸ© := hJ
  rcases hL with âŸ¨z, hzâŸ©
  obtain âŸ¨u, v, huvâŸ© := hJ
  rw [â† hz, â† mul_assoc] at huv
  obtain âŸ¨n, âŸ¨hn, hneqâŸ©âŸ© := Monoid.exists_pow_mul_of_sandwich huv
  use (u * z) ^ (n - 1) * u
  rw [â† hz, â† mul_assoc, mul_assoc _ u, â† pow_succ]
  have hl : n - 1 + 1 = n := by exact Nat.succ_pred_eq_of_ne_zero hneq
  rw [hl, hn]

-- Dual to above
@[simp] theorem REquiv.of_le_and_jEquiv (hR : x â‰¤ğ“¡ y) (hJ: x ğ“™ y) : x ğ“¡ y := by
  simp_all [â† REquiv.op_iff]

-- corrolary to `LEquiv.of_le_and_jEquiv`
@[simp] lemma LEquiv.mul_of_jEquiv (hJ : x ğ“™ y * x) : x ğ“› y * x := by
  symm
  apply LEquiv.of_le_and_jEquiv
  Â· simp
  Â· symm; exact hJ

-- corrolary to `REquiv.of_le_and_jEquiv`
@[simp] lemma REquiv.mul_of_jEquiv (hJ : x ğ“™ x * y) : x ğ“¡ x * y := by
  symm
  apply REquiv.of_le_and_jEquiv
  Â· simp
  Â· symm; exact hJ

theorem HEquiv.of_sandwich (h : x = y * x * z ) : x ğ“— x * z âˆ§ x ğ“— y * x := by
  have hLLE : x â‰¤ğ“› x * z := by
    use y
    rw [â† mul_assoc, â† h]
  have hJâ‚ : x ğ“™ x * z := by
    simp [JEquiv, hLLE]
  have hRLE : x â‰¤ğ“¡ y * x := by
    use z
    rw [â† h]
  have hJâ‚‚ : x ğ“™ y * x := by
    simp [JEquiv, hRLE]
  simp [â† HEquiv.rEquiv_and_lEquiv_iff]
  clear h -- h breaks `simp all`
  constructor <;> constructor
  Â· apply REquiv.mul_of_jEquiv hJâ‚ -- x ğ“¡ x * z
  Â· apply LEquiv.of_le_and_jEquiv hLLE hJâ‚ -- x ğ“› x * z
  Â· apply REquiv.of_le_and_jEquiv hRLE hJâ‚‚ -- x ğ“¡ y * x
  Â· apply LEquiv.mul_of_jEquiv hJâ‚‚ -- x ğ“› y * x

/-- In finite semigroups, Green's ğ““-relation equals the ğ“™-relation.
The backward implication holds even in infinite semigroups. -/
theorem D_eqv_iff_J_finite : x ğ“™ y â†” x ğ““ y := by
  constructor
  Â· intro hJ
    rcases _ : hJ with âŸ¨âŸ¨uâ‚, vâ‚, huvâ‚âŸ©, âŸ¨uâ‚‚, vâ‚‚, huvâ‚‚âŸ©âŸ©
    rw [â† huvâ‚‚] at huvâ‚
    simp_rw [â† mul_assoc, mul_assoc _ _ vâ‚] at huvâ‚
    obtain âŸ¨n, âŸ¨hn, hnPosâŸ©âŸ©  := Monoid.exists_mul_pow_of_sandwich huvâ‚
    obtain âŸ¨k, âŸ¨hk, hkPosâŸ©âŸ©  := Monoid.exists_pow_mul_of_sandwich huvâ‚
    rw [DEquiv]
    use x * vâ‚‚
    constructor
    Â· simp [REquiv] -- goal: `x â‰¤ğ“¡ x * vâ‚‚`
      use vâ‚ * (vâ‚‚ * vâ‚) ^ (n - 1)
      simp [â† mul_assoc]
      rw [mul_assoc â†‘x â†‘vâ‚‚ vâ‚, mul_assoc â†‘x (â†‘vâ‚‚ * vâ‚) _ , â† pow_succ']
      have hTriv : n - 1 + 1 = n := by exact Nat.succ_pred_eq_of_ne_zero hnPos
      rw [hTriv, hn]
    Â· subst y
      simp [LEquiv] -- goal: `x * vâ‚‚ â‰¤ğ“› y = uâ‚‚ * x * vâ‚‚`
      apply LLE.mul_right_compat
      use (uâ‚ * uâ‚‚) ^ (k - 1) * uâ‚
      rw [ â† mul_assoc, mul_assoc _ uâ‚]
      rw [â† pow_succ]
      have hTriv : k - 1 + 1 = k := by exact Nat.succ_pred_eq_of_ne_zero hkPos
      rw [hTriv, hk]
  Â· intro hD
    simp_all
