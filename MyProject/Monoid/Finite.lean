import MyProject.Monoid.Defs

/-!
# Finiteness in Semigroups
This file begins by defining an exponentiation operation for semigroups using positive natural numbers.
We then prove statments about idempotent elements in finite semigroups and monoids.
These statments are used in the latter parts of the file to prove the DJ Theorem.

## Main Definitions
- `pnatPow`: Exponentiation for semigroups over positive naturals.

## Main Statements
- `Semigroup.exists_idempotent_pow` - and a version for monoids
- `Semigroup.exists_pow_mul_of_sandwich` - and a version for monoids
- `D_eqv_iff_J_finite`: The DJ Theorem, which states that in finite Monoids,
  the ùìì-relation equals the ùìô-relation.
-/

/-
### Positive Natural Number Exponentiation for Semigroups
Exponentiation in monoids is defined for natural number exponents, but here we require non-zero
natural numbers (`‚Ñï+`) because `x^0` is not well defined in a semigroup context.

We also prove lemmas about the properties of this exponentiation operation and of the `‚Ñï+` type.

The simp-tagged lemmas collectively normalize power expressions when calling `simp`.
For example, `(a * b) ^ n * (a * b) ^ m * a ^ 1` normalizes to `a * (b * a) ^ (n + m)`.

Analogous definitions and lemmas for exponentiation in monoids can be found in
`Mathlib.Algebra.Group.Defs`.
-/

namespace PNat

/-- If `m < n` for positive naturals, then there exists a positive `k` such that `n = k + m`. -/
lemma exists_eq_add_of_lt {m n : ‚Ñï+} (m_lt_n : m < n) : ‚àÉ (k : ‚Ñï+), n = k + m := by
  use n - m
  pnat_to_nat; omega

/-- For positive naturals, if `m < n` then `m + (n - m) = n`. -/
@[simp] lemma add_sub_cancel' {n m : ‚Ñï+} (m_lt_n : m < n) : m + (n - m) = n := by
  induction m using PNat.recOn <;> (pnat_to_nat; omega)

/-- For positive naturals, `n < 2 * n * m` -/
lemma lt_2_mul_mul (n m : ‚Ñï+) : n < 2 * n * m := by
  induction m using PNat.recOn with
  | one => pnat_to_nat; omega
  | succ m ih => pnat_to_nat; ring_nf; omega

variable {S : Type*} [Semigroup S]

/-- Exponentiation for semigroups over positive naturals -/
def pnatPow (x : S) (n : ‚Ñï+) : S := @PNat.recOn n (fun _ => S) x (fun _ ih => ih * x)

/-- Provides the notation `a ^ n` for `pnatPow a n` -/
instance hPow : Pow S ‚Ñï+ := ‚ü®pnatPow‚ü©

variable (x y : S) (n m : ‚Ñï+)

/-- For any element `x` of a semigroup, `x` raised to the power `1` equals `x`. -/
@[simp] lemma pow_one : x ^ (1 : ‚Ñï+) = x := by rfl

/-- Exponentiation satisfies the successor property -/
@[simp] lemma pow_succ : (x ^ n) * x = x ^ (n + 1) := by induction n using PNat.recOn <;> rfl

/-- Power of a sum equals the product of powers -/
@[simp] lemma pow_add : x ^ m * x ^ n = x ^ (m + n) := by
  induction n using PNat.recOn with
  | one => rw [pow_one, pow_succ]
  | succ k ih => simp_rw [‚Üê add_assoc, ‚Üê pow_succ, ‚Üê mul_assoc, ih]

/-- Multiplicative associativity for powers -/
@[simp] lemma mul_pow_mul : (x * y) ^ n * x = x * (y * x) ^ n := by
  induction n using PNat.recOn with
  | one => simp [‚Üê mul_assoc]
  | succ n ih => simp only [‚Üê pow_succ, ‚Üê mul_assoc, ih]

/-- Powers of the same element commute with each other -/
lemma pow_mul_comm : x ^ m * x ^ n = x ^ n * x ^ m := by rw [pow_add, add_comm, pow_add]

/-- For every `x : S` and `n : ‚Ñï+`, the power `x ^ n` commutes with `x` -/
@[simp] lemma pow_mul_comm' : x ^ n * x = x * x ^ n := by
  induction n using PNat.recOn with
  | one    => rfl
  | succ k ih => rw [‚Üê pow_succ, ‚Üê mul_assoc, ih]

/-- The power of a power equals the power of the product of exponents -/
@[simp] lemma pow_mul : (x ^ n) ^ m = x ^ (m * n) := by
  induction m using PNat.recOn with
  | one    => rw [one_mul, pow_one]
  | succ k ih => simp_rw [‚Üê pow_succ, ih, pow_add]; congr; ring

/-- Right-associated powers commute -/
lemma pow_right_comm : (x ^ m) ^ n = (x ^ n) ^ m := by simp only [pow_mul, mul_comm]

/-- Bridge between PNat powers and standard Nat powers in monoids -/
lemma pow_pnat_to_nat {M} [Monoid M] (x : M) (n : ‚Ñï+) :
    x ^ n = x ^ (n : ‚Ñï) := by
  induction n with
  | one => simp [PNat.pnatPow]
  | succ n' ih =>
    rw [PNat.add_coe]; simp
    rw [ ‚Üê PNat.pow_succ, ‚Üê Nat.succ_eq_add_one, _root_.pow_succ, ih]

end PNat

/-! # Idempotent Elements in Finite Semigroups -/

namespace Semigroup

variable {S : Type*} [Semigroup S]

/-- If `x` is idempotent, then raising `x` to any positive power yields `x`. -/
lemma pow_succ_eq {x : S} (n : ‚Ñï+) (h_idem : IsIdempotentElem x) : x ^ n = x := by
  induction n using PNat.recOn with
  | one    => rfl
  | succ n' ih => rw [‚Üê PNat.pow_succ, ih, h_idem]

/-- If `S` is finite then for any `x : S` there exist
exponents such that the power eventually repeats. -/
lemma exists_repeating_pow [Finite S] (x : S) : ‚àÉ (m n : ‚Ñï+), x ^ m * x ^ n = x ^ m := by
  obtain ‚ü®o, p, hop, heq‚ü© : ‚àÉ o p : ‚Ñï+, o ‚â† p ‚àß x ^ o = x ^ p := by
    apply Finite.exists_ne_map_eq_of_infinite
  simp_all only [ne_eq, PNat.pow_add]; pnat_to_nat
  rcases (Nat.lt_or_gt_of_ne hop) with (o_lt_p | p_lt_o)
  ¬∑ use o, p - o; simp_all
  ¬∑ use p, o - p; simp_all

/-- If two powers of an element `x : S` are idempotent, then they are equal. -/
theorem pow_idempotent_unique {x : S} {m n : ‚Ñï+}
(hm : IsIdempotentElem (x ^ m)) (hn : IsIdempotentElem (x ^ n)) : x ^ m = x ^ n := by
  rw [‚Üê pow_succ_eq n hm, PNat.pow_right_comm, pow_succ_eq m hn]

/-- In a finite semigroup `S`, the subsemigroup generated by any element contains an idempotent -/
theorem exists_idempotent_pow [Finite S] (x : S) : ‚àÉ (m : ‚Ñï+), IsIdempotentElem (x ^ m) := by
  -- `n` is the length of the pre-period (tail),
  --`loop_size` is the length of the cycle.
  obtain ‚ü®n, loop_size, loop_eq‚ü© := exists_repeating_pow x
  -- The `loop` lemma formalizes that once powers of `a` enter the cycle,
  -- adding further multiples of `loop_size` to the exponent doesn't change the result.
  have loop : ‚àÄ (loop_count start : ‚Ñï+),
      n < start ‚Üí x ^ (start + loop_count * loop_size) = x ^ start := by
    intro loop_count
    induction loop_count using PNat.recOn with
    | one =>
      intro start n_lt_start
      obtain ‚ü®diff, hdiff‚ü© := PNat.exists_eq_add_of_lt n_lt_start
      simp_all only [PNat.pow_add, one_mul, ‚Üê PNat.pow_add, mul_assoc]
    | succ loop_count' ih =>
      intro start n_lt_start
      obtain ‚ü®diff, hdiff‚ü© := PNat.exists_eq_add_of_lt n_lt_start
      subst hdiff
      specialize ih (diff + n)
      apply ih at n_lt_start
      have h_arith :
        (loop_count' + 1) * loop_size = (loop_count' * loop_size) + loop_size := by ring
      simp_rw [h_arith, ‚Üê add_assoc, ‚Üê PNat.pow_add] at *
      rw [n_lt_start, mul_assoc, loop_eq]
  -- We choose `2 * n * loop_size` as the exponent for our idempotent element.
  -- This ensures the exponent is beyond the pre-period `n` and is a multiple of `loop_size`.
  use 2 * n * loop_size
  unfold IsIdempotentElem
  specialize loop (2 * n) (2 * n * loop_size)
  simp_all only [PNat.pow_add]
  -- Apply the `loop` lemma. The condition `n < 2 * n * loop_size` is met by `PNat.lt_2_mul_mul`.
  apply loop
  exact PNat.lt_2_mul_mul n (loop_size)

lemma exists_pow_mul_of_sandwich [Finite S] (x y z : S) (h : x * y * z = y) :
    ‚àÉ n : ‚Ñï+,  x ^ n * y = y := by
  have loop : ‚àÄ m : ‚Ñï+, x ^ m * y * z ^ m = y := by
    intro m
    induction m using PNat.recOn with
    | one => simp_all
    | succ m ih =>
      simp [‚Üê PNat.pow_succ]
      rw [‚Üê PNat.pow_mul_comm', ‚Üê mul_assoc, mul_assoc (x ^ m), mul_assoc (x ^ m)]
      rw [h, ih]
  -- Apply `loop` to the idempotent power of `x`
  obtain ‚ü®k, hk‚ü© := exists_idempotent_pow x
  use k
  have hLoop := loop k
  rw [‚Üê hLoop]
  unfold IsIdempotentElem at hk
  simp_rw [‚Üê mul_assoc, hk]

open MulOpposite

-- DUal to above
lemma exists_mul_pow_of_sandwich [Finite S] (x y z : S) (h : x * y * z = y) :
    ‚àÉ n : ‚Ñï+, y * z ^ n = y := by
  have loop : ‚àÄ m : ‚Ñï+, x ^ m * y * z ^ m = y := by
    intro m
    induction m using PNat.recOn with
    | one => simp_all
    | succ m ih =>
      simp [‚Üê PNat.pow_succ]
      rw [‚Üê PNat.pow_mul_comm', ‚Üê mul_assoc, mul_assoc (x ^ m), mul_assoc (x ^ m)]
      rw [h, ih]
  obtain ‚ü®k, hk‚ü© := exists_idempotent_pow z
  use k
  have hLoop := loop k
  rw [‚Üê hLoop]
  unfold IsIdempotentElem at hk
  rw [mul_assoc, hk]

end Semigroup

/-! We now give versions of the previous proofs for Finite Monoids -/

namespace Monoid

variable {M : Type*} [Monoid M] [Finite M]

/-- Every element in a finite monoid has a non-zero idempotent power -/
theorem exists_idempotent_pow (x : M) :
    ‚àÉ (n : ‚Ñï), IsIdempotentElem (x ^ n) ‚àß n ‚â† 0:= by
  obtain ‚ü®m, hm‚ü© := Semigroup.exists_idempotent_pow x
  use m; simp_all only [IsIdempotentElem]
  constructor
  ¬∑ rwa [‚Üê PNat.pow_pnat_to_nat]
  ¬∑ simp [PNat.ne_zero]

lemma exists_pow_mul_of_sandwich {x y z : M} (h : x * y * z = y) :
    ‚àÉ n : ‚Ñï,  x ^ n * y = y ‚àß n ‚â† 0 := by
  obtain ‚ü®n, hn‚ü© := Semigroup.exists_pow_mul_of_sandwich x y z h
  use n
  constructor
  ¬∑ simp_all [‚Üê PNat.pow_pnat_to_nat]
  ¬∑ simp_all [PNat.ne_zero]

-- dual to above
lemma exists_mul_pow_of_sandwich {x y z : M} (h : x * y * z = y) :
    ‚àÉ n : ‚Ñï, y * z ^ n = y ‚àß n ‚â† 0 := by
  obtain ‚ü®n, hn‚ü© := Semigroup.exists_mul_pow_of_sandwich x y z h
  use n
  constructor
  ¬∑ simp [‚Üê PNat.pow_pnat_to_nat, hn]
  ¬∑ simp

end Monoid

/-! ### DJ Theorem -/

variable {M : Type*} [Monoid M] {x y z : M}

/-- Every ùìì-related pair is ùìô-related. -/
@[simp] theorem JEquiv.of_dEquiv (h : x ùìì y): x ùìô y := by
  simp_all [DEquiv, JEquiv]
  rcases h with ‚ü®z, ‚ü®‚ü®t, hR‚ÇÅ‚ü©, ‚ü®u, hR‚ÇÇ‚ü©‚ü© , ‚ü®‚ü®v, hL‚ÇÅ‚ü©, ‚ü®w, hL‚ÇÇ‚ü©‚ü©‚ü©
  constructor
  ¬∑ use v, t
    rw [‚Üê hR‚ÇÅ, ‚Üê hL‚ÇÅ]
  ¬∑ use w, u
    rw [‚Üê hL‚ÇÇ, ‚Üê hR‚ÇÇ, ‚Üê mul_assoc]

variable [Finite M]

@[simp] theorem LEquiv.of_le_and_jEquiv (hL : x ‚â§ùìõ y) (hJ: x ùìô y) : x ùìõ y := by
  simp_all [LEquiv]
  obtain ‚ü®_, hJ‚ü© := hJ
  rcases hL with ‚ü®z, hz‚ü©
  obtain ‚ü®u, v, huv‚ü© := hJ
  rw [‚Üê hz, ‚Üê mul_assoc] at huv
  obtain ‚ü®n, ‚ü®hn, hneq‚ü©‚ü© := Monoid.exists_pow_mul_of_sandwich huv
  use (u * z) ^ (n - 1) * u
  rw [‚Üê hz, ‚Üê mul_assoc, mul_assoc _ u, ‚Üê pow_succ]
  have hl : n - 1 + 1 = n := by exact Nat.succ_pred_eq_of_ne_zero hneq
  rw [hl, hn]

-- Dual to above
@[simp] theorem REquiv.of_le_and_jEquiv (hR : x ‚â§ùì° y) (hJ: x ùìô y) : x ùì° y := by
  simp_all [‚Üê REquiv.op_iff]

-- corrolary to `LEquiv.of_le_and_jEquiv`
@[simp] lemma LEquiv.mul_of_jEquiv (hJ : x ùìô y * x) : x ùìõ y * x := by
  symm
  apply LEquiv.of_le_and_jEquiv
  ¬∑ simp
  ¬∑ symm; exact hJ

-- corrolary to `REquiv.of_le_and_jEquiv`
@[simp] lemma REquiv.mul_of_jEquiv (hJ : x ùìô x * y) : x ùì° x * y := by
  symm
  apply REquiv.of_le_and_jEquiv
  ¬∑ simp
  ¬∑ symm; exact hJ

theorem HEquiv.of_sandwich (h : x = y * x * z ) : x ùìó x * z ‚àß x ùìó y * x := by
  have hLLE : x ‚â§ùìõ x * z := by
    use y
    rw [‚Üê mul_assoc, ‚Üê h]
  have hJ‚ÇÅ : x ùìô x * z := by
    simp [JEquiv, hLLE]
  have hRLE : x ‚â§ùì° y * x := by
    use z
    rw [‚Üê h]
  have hJ‚ÇÇ : x ùìô y * x := by
    simp [JEquiv, hRLE]
  simp [‚Üê HEquiv.rEquiv_and_lEquiv_iff]
  clear h -- h breaks `simp all`
  constructor <;> constructor
  ¬∑ apply REquiv.mul_of_jEquiv hJ‚ÇÅ -- x ùì° x * z
  ¬∑ apply LEquiv.of_le_and_jEquiv hLLE hJ‚ÇÅ -- x ùìõ x * z
  ¬∑ apply REquiv.of_le_and_jEquiv hRLE hJ‚ÇÇ -- x ùì° y * x
  ¬∑ apply LEquiv.mul_of_jEquiv hJ‚ÇÇ -- x ùìõ y * x

/-- In finite semigroups, Green's ùìì-relation equals the ùìô-relation.
The backward implication holds even in infinite semigroups. -/
theorem D_eqv_iff_J_finite : x ùìô y ‚Üî x ùìì y := by
  constructor
  ¬∑ intro hJ
    rcases _ : hJ with ‚ü®‚ü®u‚ÇÅ, v‚ÇÅ, huv‚ÇÅ‚ü©, ‚ü®u‚ÇÇ, v‚ÇÇ, huv‚ÇÇ‚ü©‚ü©
    rw [‚Üê huv‚ÇÇ] at huv‚ÇÅ
    simp_rw [‚Üê mul_assoc, mul_assoc _ _ v‚ÇÅ] at huv‚ÇÅ
    obtain ‚ü®n, ‚ü®hn, hnPos‚ü©‚ü©  := Monoid.exists_mul_pow_of_sandwich huv‚ÇÅ
    obtain ‚ü®k, ‚ü®hk, hkPos‚ü©‚ü©  := Monoid.exists_pow_mul_of_sandwich huv‚ÇÅ
    rw [DEquiv]
    use x * v‚ÇÇ
    constructor
    ¬∑ simp [REquiv] -- goal: `x ‚â§ùì° x * v‚ÇÇ`
      use v‚ÇÅ * (v‚ÇÇ * v‚ÇÅ) ^ (n - 1)
      simp [‚Üê mul_assoc]
      rw [mul_assoc ‚Üëx ‚Üëv‚ÇÇ v‚ÇÅ, mul_assoc ‚Üëx (‚Üëv‚ÇÇ * v‚ÇÅ) _ , ‚Üê pow_succ']
      have hTriv : n - 1 + 1 = n := by exact Nat.succ_pred_eq_of_ne_zero hnPos
      rw [hTriv, hn]
    ¬∑ subst y
      simp [LEquiv] -- goal: `x * v‚ÇÇ ‚â§ùìõ y = u‚ÇÇ * x * v‚ÇÇ`
      apply LLE.mul_right_compat
      use (u‚ÇÅ * u‚ÇÇ) ^ (k - 1) * u‚ÇÅ
      rw [ ‚Üê mul_assoc, mul_assoc _ u‚ÇÅ]
      rw [‚Üê pow_succ]
      have hTriv : k - 1 + 1 = k := by exact Nat.succ_pred_eq_of_ne_zero hkPos
      rw [hTriv, hk]
  ¬∑ intro hD
    simp_all
